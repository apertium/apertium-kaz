
# ========== #
# Delimiters #
# ========== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;

# ============= #
# Tags and sets #
# ============= #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

# First-level/Parts-of-speech tags
# --------------------------------

LIST A = adj ;
LIST Adv = adv ;
LIST Pron = prn ;
LIST N = n ;
LIST Prop = np ;
LIST V = v ;
LIST Vaux = vaux ;
LIST Cop = cop ;
LIST Det = det ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;
LIST Num = num ;
LIST Post = post ;
LIST Postadv = postadv ;
LIST Cm = cm ;

# POS sub-categories
# ------------------

LIST Pers = pers ;
LIST Interr = itg ;

# "Syntactic" tags
# ----------------

LIST Advl = advl ;
LIST Attr = attr ;
LIST Subst = subst ;

# Morphosyntactic properties   # if you find yourself embracing the same tag in
# --------------------------   # brackets over and over again, you may want to
                               # add it here

LIST Sg = sg ;
LIST Pl = pl ;

LIST Nom = nom ;
LIST Dat = dat ;
LIST Acc = acc ;
LIST Abl = abl ;
LIST Loc = loc ;
LIST Ins = ins ;
LIST P3 = p3 ;

# Semantic lists
# --------------

LIST MOVEMENT-V = "жүгір" ;

LIST MOVEMENT-ADV = "тиз" "акрын" "әкрен" ;

# All possible word categories
# ----------------------------

SET WORD = N | V | A | Post | Postadv | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

SET PRE-N =  A | Det | Postadv | Num | (n gen) | (prn gen) | CC | (attr) ;

SET MARK =  Cm | ("\\") | ("\;") | ("\(") ;

SET WORDMARK = WORD | MARK ;

SET N-MOD = A | Det | Num | (n gen) | (prn gen) ;

SET ADJ-MOD = Postadv | Adv ;

SET NPMARK = N-MOD | ADJ-MOD ;

# Categories which cannot be part of a noun phrase
# ------------------------------------------------

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Noun sets
# ---------

LIST N-SG-NOM = (n sg nom) ;
LIST N-PL-NOM = (n pl nom) ;

# Pronoun sets
# ------------

SET PRN-PERS = (prn pers) ;

# Adjective sets
# --------------

LIST A/Advl = (adj advl) ;

LIST A/Subst = (adj subst nom) ;

SET A1 = A/Advl | A/Subst ;

# Verb sets
# ---------

LIST FiniteVerb = pres aor past ifi ifi_evid fut fut_plan imp opt;

LIST V-P3 = (v p3) (vaux p3) (cop p3) ;

LIST Gerund = ger ger_ppot ger_past ger_perf ger_impf ger_abs ;

SET V-NotGerund = V - Gerund ;

LIST Participle = prc_perf prc_impf prc_cond prc_vol ;

LIST VerbalAdverb = gna_perf gna_cond gna_until gna_after ;

LIST Copula = (cop p3 sg) (cop p3 pl) ;

LIST FourAuxiliaries = "тұр" "жүр" "отыр" "жатыр" ;

# Barriers
# --------

SET S-BOUNDARY = CS | Interr | EOS ;


# ======= #
  SECTION
# ======= #


# some of the rules having nouns as constraints didn't work when the noun has
# personal copulas as subreadings, so the rule removing personal copulas is
# placed into a separate section, before any other rules

#
 REMOVE Copula IF
        (NOT 1 EOS OR MARK)
;
##


# Remove copula here
## АРАЛ ТЕҢІЗІ.


# ======= #
  SECTION
# ======= #



# Part-of-speech marking
# ======================



#
 SELECT FiniteVerb IF
        (1 EOS OR ("де"i))               # FIXME s/де/SentenceBoundary/
        (NOT 0 ("шығар"i) OR ("бар"i))   # FIXME a better way?
;
## (!) 40 . Мүмкін бұл Азамат [0]шығар?
##  (!) 13 . Жоқ, Айгүлдің күшігі жоқ, оның мысығы [0]бар.
## Мағынасы төңкеріліп [0]кетті деп ойламағаным үшін ғафу етіңіз.


SELECT Adv IF (0 MOVEMENT-ADV) (0 A OR Adv) (1* MOVEMENT-V BARRIER S-BOUNDARY);


#
 SELECT Interj IF
         (-1 BOS)
         (1 Cm)
;
## "Мысалы, ежелгі заманның өзінде Арал теңізі көп елдерге мәлім болған."


#
 REMOVE Post IF
         (NOT -1 N OR Prop OR Pron OR Subst OR Gerund)
;
##



# Determiners
# ===========



"<бір>" SELECT Det IF (-1C A) (1 N);
    #! Әхмәт тиз генә иске зур _бер_ агачка йөгерә.
    #! Әхмәт акрын гына иске зур _бер_ агачка йөгерә.

"<бар>" SELECT V IF (-1 Dat) ;
"<бар>" SELECT A IF (-1 (px3sp)) ;
"<бар>" SELECT Det IF (1* NPMARK BARRIER N OR MARK) ;
# 172. Сот арқылы немесе өзгеде осындай құзыреті _бар_ мемлекеттік органдардың
# шешімі арқылы тәркіленген мұражайлық маңызы бар жинақтардың немесе жекелеген заттардың есебінен;
#! Бар<det> җирдән карый, әмма Әхмәтне таба алмый.
#! Акча бармы? Бар<adj>.
#! Акча бармы? Бар<adj>, ләкин бик аз!


# FIXME removes determiner even when there is a noun 1 to the right
#REMOVE Det IF
#       ((NEGATE 1 N OR Subst) OR (NEGATE 1 A LINK 1 N OR Subst))
#;
## 44 . Ол енді ол дыбысты анығырақ ести бастады.
## (!) 34 . Ол Азаматтың қайда екенін білсе де айтқысы келген жоқ.


#
 SELECT Pron IF
        (0C Det OR Pron)
        (1 Adv)
;
##



# Nouns
# =====



 REMOVE Attr IF
       (0 N OR Prop)
       (NOT 0 Loc)
       (NOT 1 N OR Prop)
;
## 176. Орталықта Түркістанның төл тарихына арналған музейлік [0]экспозиция жасақталған.
## (!) 184 . Үшінші қабатта «Тәуелсіз [0]Қазақстандағы жаңа Түркістан» атты экспозициялар қойылған.


# Select attributive reading of the first noun of the II izafet construct
# (as Tatar grammars call it)
 SELECT Attr IF
        (0C N OR Prop)
        (1 N OR Prop OR Subst)
        (1 (px3sp))
;
# 111. Түркияның оңтүстік-шығысындағы [0]Газиантеп қаласында кеше болған жарылыстан қаза тапқандар саны 9-ға жетті.


# If a former gerund or verbal adjective was lexicalized as a noun, select noun
 SELECT N IF
        (0 N)
        (0 Gerund  OR (gpr_pot))
;
# professions, such as "оқушы"; -U gerunds , in theory other gerunds too



# Adjectives
# ==========



# select adverbial reading of adjectives if any verbal form except gerund follows
 SELECT Advl IF
         (1C V-NotGerund)
;
#

# select pure adjective reading ("predicative" as it is called) of adjective if sentence ends
 REMOVE Advl IF
        (0 A1)
        (1 EOS)
;
 REMOVE Subst IF
         (0 A1)
         (1 EOS)
;
#



# Postpositions
# =============



#
"<қатар>" SELECT Post IF
                 (-1 Pron + Ins)
;
## 174 . Орталық сонымен [0]қатар оқушыларға тәрбие беруде де маңызды рөл атқарады.
## 148 . Ол АҚШ үкіметін WikiLeaks сайтын қудалауын тоқтатуға шақырып,
## өзін ресейлік Pussy Riot панк тобымен және New York Times басылымымен [0]қатар қойды.


#
 "<қарай>" SELECT Post IF
        (-1 Dat)
;
##


#
 "<дейін>" SELECT Post IF
        (-1 Dat)
;
##


#
 "<бері>" SELECT Post IF
        (-1 Abl)
;
##



# Verbs
# =====



#
 REMOVE Participle IF
        (NOT 1C V OR Vaux)
;
##


#
 SELECT Participle IF
        (0C Participle OR VerbalAdverb)
        (1 Vaux)
;
## 16 . Азамат ескі үлкен бір ағашқа қарай қатты [?]жүгіріп бара жатыр, ол сол
## ағаштың артына Айгүлден [0]жасырынып жатыр.

#
 REMOVE Vaux IF
        (NOT -1 Participle)
;
## 187 . Біздің дәуірімізден 1 миллион жыл бұрын өмір сүрген "тік жүретін адамнан" [0]бастап, өз замандастарына дейінгі
## кезеңдерді қамтитын, ... орталықтың экспозициясы негізгі сегіз ірі бөлімнен тұрады.


# [0] is auxilliary, because a participle preceedes, and [0] is not in imperative,
# because there is a subject (i.e. something in nominative closest to the
# beginning of the sentence)
 SELECT (vaux p3) IF
        (0 FourAuxiliaries)
        (-1 (prc_perf))
        (@1 Nom)
;
 SELECT (vaux p3) IF
        (0 FourAuxiliaries)
        (-1 (prc_perf))
        (-1* (prn nom) BARRIER BOS)
;
#* 15. Олардың анасы мысығымен бірге үйде, ол терезеден Азамат пен Айгүлдің ойнағанына қарап [0]тұр.


# 2p sg imperative form is a bare stem, so it is often ambigious with other parts of speech
 REMOVE (imp) IF
        (NOT 1 EOS OR MARK OR ("де"))
;
##  96 . Ал оң жақ жағалауы батпақты, онда [0]қалың орман өскен.
## [0]Бар деп айттым мен оған.
 REMOVE (imp) IF
        (-1* Adv OR Advl OR Loc BARRIER BOS)
        (@1 Nom LINK NOT @2 Cm)
;
## 35 . Айгүл бақшаның ішінде ақырын басып жүр.
## 35*. Айгүл, бақшаның ішінде ақырын басып жүр.
## Сен, бақшаның ішінде ақырын басып жүр.
## Сен үйде тұр.
## Кітап ал.


# FIXME extend/generalize
 SELECT (gpr_past) IF
        (0C (ger_past) + Nom)
        (1 N)
;
##  172 . Сот арқылы немесе өзгеде осындай құзыреті бар мемлекеттік органдардың
## шешімі арқылы тәркіленген мұражайлық маңызы бар жинақтардың немесе [0]жекелеген заттардың есебінен;

# Wordform/lemma-specific rules   # Adding new stuff here is discouraged.
# =============================   # Try to generalize, if possible, existing rules.



#
 "<өте>" SELECT Adv IF
                (1 A)
;
## Бірақ кеше _өте_ суық еді!


#
 "<кейін>" SELECT Post IF
                (-1 Abl)
;
## 23. Айгүл санап біткеннен кейін айналасына қарады.


# ugly and RISKY
 "<сол>" SELECT Det IF
        (NOT 1 ("қол"i) OR ("жақ"i))
;
## (!) Күйеуім - сол жақтың қазағы.


#
 "<аса>" SELECT Adv IF
        (1 A)
;
## "Әл-Истахри келтірген мәліметтер аса құнды саналады."


#
 "<ал>" REMOVE CC IF
        (NOT -1 BOS OR Cm )
;
## Бөлмеде шкаф бар, содан бiр кiтап ал.


# FIXME test
 SELECT Pron IF
        (0 ("ешкі"i))
        (0 ("ешкім"i))
        (1* ("жоқ"i) OR (v neg) BARRIER EOS)
;
## 70. Оның рекордын әзірше ешкім бұза алған жоқ.


#
 "<жылы>" SELECT N IF
        (-1 Num)
;
##


#
 "<қарасты>" SELECT A IF
        (-1 (dat))
;
## 177. Орталыққа _қарасты_ Түркістан қаласында Н.Оңдасынов атындағы
## тарихи-мемориалдық мұражайы атты филиал жұмыс жасайды.


#
 "<қайта>" SELECT Adv IF
        (1C V)
;
## 41 . Ол дыбыс қайта естіледі!
## Ол қайта алмайды.


#
 "<енді>" SELECT Adv IF
        (1* Acc BARRIER EOS)
        (1* V BARRIER EOS)
;
##


#
 "<көптеген>" SELECT Det IF
        (1 N + Pl)
;
##


# ambigious with v.pass
 "<құрал>" SELECT N IF
        (-1 A OR (n attr))
;
##


# UGLY
# "сый" is certainly not <v.iv.imp> in the following example,
# I don't think that сый<imp> is ever used
 "<сый>" SELECT N
;
# 171. Жеке және заңды тұлғалардан _сый_ немесе мұра ретінде қабылданған заттар есебінен;


# imperative of "ісу" ("to swell") - just like "<сый>" above sounds odd,
# but let's try to deal with it in a less ugly way
 "<іс>" SELECT N IF
        (-1 A)
;
# 178. ...тәрбиелік мәні бар іс-шараларды бірге ұйымдастыру...


# FIXME: could be achieved with a more general rule I think
 "<көрме>" SELECT N IF
        (1 ("зал"i))
;
## 181. Ғимарат 3 қабаттан, 5 көрме залынан, конференция залынан, қолөнершілердің шеберханасы...


#
 SELECT N IF
        (0 ("же"i) + (gpr_fut))
        (0 ("жер"i) + N)
;
## 24 . Ол "Азамат қай [0]жерге кетті?  Оны көрдіңдер ме?" деп іздеп жатыр.

"<қарайды>" SELECT ("қара") IF
                   (-1 Dat)
;
## 30 . Айгүл терезеде тұрған анасына қарайды.


#
 SELECT CC IF
        (0 ("сондай-ақ"i))
        (-1 BOS OR Cm)
;
## 87 . Сондай-ақ, Арал теңізінің көлемі жайлы, Арал теңізі жайлы ұғымдар сонау ерте дүние әдебиеттерінде де кездеседі.


#
 SELECT N IF
        (0 ("құрама"i))
        (-1 PRE-N)
;
## 76 . Қазақстандық құрама «А» тобында соңғы орында қалып қойды.


#
 SELECT N IF
        (0 ("қор"))
        (-1 N)
;
## 169. Музей [1]қоры Қазақстан Республикасы Мәдениет және ақпарат министрінің...


#
 SELECT A IF
        (0 ("құрама"))
        (-1 (np top))
;
## 69. Франция құрамасының бұрынғы ойыншысы Мишель Платини...


# FIXME: I wanted to refer "жәдігер" as a lemma, but it didn't work
# (because it has cop p3 subreding I guess)
 "<жинағы>" SELECT N IF
        (-1 ("<жәдігерлер>"))
;
## 165. ...тарихи жәдігерлер жинағы арқылы көрсету.


###################   TEMPORARY HACKS TO GET THE STORY TRANSLATED   ####################
###################   SOME SHOULD BE REWRITTEN LATER                ####################

REMOVE N IF (0 ("ақырын"));
    ## Айгүл оны _ақырын_ тыңдайды.

SELECT A/Advl IF (0 ("ақырын"));
    ## Айгүл оны _ақырын_ тыңдайды.

REMOVE N IF (0 ("жылы")) (NOT -1 N OR Num) ;
    #! Ауа райы бүгін әбден жақсы, ^жылы/жыл<n><px3sp><nom>/жылы<adj>/жылы<adj><subst><nom>.

###################   TO BE DONE   ####################

# Deciding about the number of a verb or copula in the 3 person
# Basic idea is to remove plural reading if subject is not in plural,
# but there a lot of corner cases, which lead to lots of mispredictions.
# So let's just delete plural reading for now.
#
# In Tatar, when the subject indicates the number, this LAR affix for the verbs
# in 3 person is kind of optional too, so singular reading will work/is right for
# the majority of the cases.
#
# Selecting plural reading might be more productive.
 REMOVE Pl IF
        (0 V OR Vaux OR Cop)
        (0 P3)
;
##

###################################### TO BE SORTED ############################


# An adjective ending with -LI is ambiguous with: 1) a noun or adjective in
# accusative, and 2) with  a verb in <ifi> form.
 REMOVE Acc IF
        (0 (n acc) OR (adj subst acc))
        (0 A)
        (NOT 1 V)
;
 REMOVE V IF
        (0 A)
        (0 (ifi))
        (NOT 1 EOS OR Cm)
;
 "<атты>" SELECT A IF
                 (1 N)
;
## 53 . Еуро-2012 туралы [0]қызықты статистикаға көз салыңыз.
## 184 . Үшінші қабатта «Тәуелсіз Қазақстандағы жаңа Түркістан» [0]атты экспозициялар қойылған.


#
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:]+[:lower:]*"r))
        (NOT -1 BOS)  ;
## 108. Өйткені бүгінгі _Арал_ деген атау сол XVII ғасырдан бергі жерге берілген.


#
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:][:upper:]+"r))
## АЗАМАТ ҚАЙДА?


# ========== #
# Delimiters #
# ========== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;

# ============= #
# Tags and sets #
# ============= #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

# Parts of speech
# ---------------

LIST A = adj ;
LIST Adv = adv ;
LIST Pron = prn ; 
LIST N = n ; 
LIST V = v ;
LIST Det = det ; 
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;
LIST Num = num ; 
LIST Post = post ; 
LIST Postadv = postadv ; 
LIST Cm = cm ; 

# POS sub-categories
# ------------------

LIST Pers = pers ;
LIST Interr = itg ;

# "Syntactic" tags
# ----------------

LIST Advl = advl ;
LIST Pred = pred ;
LIST Attr = attr ;
LIST Subst = subst ;

# Morphosyntactic properties
# --------------------------

LIST Sg = sg ;
LIST Pl = pl ;

List Acc = acc ;

# Semantic lists 
# --------------
LIST MOVEMENT-V = "йөгер" ; 

LIST MOVEMENT-ADV = "тиз" "акрын" "әкрен" ; 

# All possible word categories
# ----------------------------

SET WORD = N | V | A | Post | Postadv | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

SET PRE-N =  A | Det | Postadv | Num | (n gen) | (prn gen) | CC ; 

SET MARK =  Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

SET N-MOD = A | Det | Num | (n gen) | (prn gen) ;

SET ADJ-MOD = Postadv | Adv ;

SET NPMARK = N-MOD | ADJ-MOD ;

SET A-ADVL = (adj advl) ;

# SET A-SUBST = (adj subst) ;

# SET A1 = A | A-ADVL | A-SUBST ;

SET PRN-PERS = (prn pers) ;

# Categories which cannot be part of a noun phrase 
# ------------------------------------------------

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Noun sets
# ---------

LIST N-SG-NOM = (n sg nom) ;
LIST N-PL-NOM = (n pl nom) ;

# Verb sets
# ---------

LIST V-P3 = (v TD aor p3 sg) (v TD neg aor p3 sg) (v TD ifi p3 sg)
            (v TD aor p3 pl) (v TD neg aor p3 pl) (v TD ifi p3 pl)
            (v tv ifi p3 sg) (v tv aor p3 sg) (v tv prc_vol  p3 sg)
            (v tv ifi p3 pl) (v tv aor p3 pl) (v tv prc_vol  p3 pl)
            (v iv aor p3 sg) (v iv ifi p3 sg) (v TD  gna_cond p3  sg) (v tv  gna_cond p3  sg) 
            (v iv aor p3 pl) (v iv ifi p3 pl) (v TD  gna_cond p3  pl) (v tv  gna_cond p3  pl)            
            (vaux pres p3 sg) (vaux neg ifi p3 sg)
            (vaux pres p3 pl) (vaux neg ifi p3 pl)
            (cop neg p3 sg)  (cop ifi p3 sg) 
            (cop neg p3 pl)  (cop ifi p3 pl) ;

               # FIXME: I suppose that this ^ is ugly, but right now I dont know
               # how else it can be done. It'll be improved as I go along with CG
               # a bit more. Any feedback appreciated ;) /I.S./
 
# Barriers 
# --------

SET S-BOUNDARY = CS | Interr | EOS ; 

SECTION

SELECT Adv IF (0 MOVEMENT-ADV) (0 A OR Adv) (1* MOVEMENT-V BARRIER S-BOUNDARY);
SELECT A IF (0 A OR Adv) (1* N BARRIER NPNHA); 

# Determiners 
# -----------

SELECT Det IF (-1C A) (1 N); 
    #! Әхмәт тиз генә иске зур бер агачка йөгерә.
    #! Әхмәт акрын гына иске зур бер агачка йөгерә.

SELECT Det IF (0 V OR Det OR Pred OR N) (1* NPMARK BARRIER N) ;
    #! пример : Бар<det> җирдән карый, әмма Әхмәтне таба алмый.
    #! Акча бармы? Бар<pred>.
    #! Акча бармы? Бар<pred>, ләкин бик аз!

# Nouns 
# -----

REMOVE Attr IF (NOT 1 N); 

# Wordform specific    # Adding new stuff here is discouraged.  
# -----------------    # Try to generalize, if possible, existing rules.

SELECT Adv IF (0 ("өте")) (1 A);
    #! Бірақ кеше өте^өте/өт<v><TD><prc_impf>/өте<adv>/өте<v><TD><imp><p2><sg>$ суық еді!

# RISKY
REMOVE Acc IF (0 ("қызықты")) (NOT 1 V);
    #! Еуро-2012 ^туралы/туралы<post>$ ^қызықты/қызық<adj><subst><acc>/қызықты<adj>/қызықты<adj><advl>/қызықты<adj><subst><nom>$ ^статистикаға/статистика<n><dat>$ көз салыңыз.
    #! Еуро-2012 ^туралы/туралы<post>$ ^қызықты/қызықты<adj>/қызықты<adj><advl>/қызықты<adj><subst><nom>$ ^статистикаға/статистика<n><dat>$ көз салыңыз.

###################   TEMPORARY HACKS TO GET THE STORY TRANSLATED   ####################
###################   SOME SHOULD BE REWRITTEN LATER                ####################

REMOVE N IF (0 ("ақырын"));
    # a temporary ugly rule for translating the story 

SELECT A-ADVL IF (0 ("ақырын"));
    # a temporary ugly rule for translating the story 
    # (1* V BARRIER S-BOUNDARY) can be added after the following verb is fixed
    ## Айгүл оны ақырын<adj><advl> тыңдайды.

REMOVE N IF (0 ("жылы")) (NOT -1 N) ;
    # a temporary ugly rule for translating the story 
    #! Ауа райы бүгін әбден жақсы, ^жылы/жыл<n><px3sp><nom>/жылы<adj>/жылы<adj><subst><nom>.

###################   TO BE DONE   ####################

# get rid of plural verb reading if subject is not plural

REMOVE Pl IF (0 V-P3) (NOT -1* N-PL-NOM BARRIER BOS) ;

    ## FIXME:EXTEND: subject can be a pronoun too. Or a substantivized adjective.
    ## But start with (personal) pronouns.
    #! Бірақ кеше өте суық ^еді/е<cop><ifi><p3><pl>/е<cop><ifi><p3><sg>$^!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!   M O R P H O L O G I C A L · T R A N S D U C E R · F O R · K A Z A K H   !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! See http://wiki.apertium.org/wiki/Turkic_lexicon

!=================!
 Multichar_Symbols
!=================!

! Followed http://wiki.apertium.org/wiki/Turkic_languages

! Part of speech categories = First-level tags
%<n%>         ! Noun                       ! Зат есім
%<np%>        ! Proper noun                ! Жалқы есім
%<adj%>       ! Adjective                  ! Сын есім
%<num%>       ! Numeral                    ! Сан есім
%<prn%>       ! Pronoun                    ! Есімдік
%<det%>       ! Determiner                 ! Детерминатив
%<v%>         ! Verb                       ! Етістік
%<vaux%>      ! Auxilary verb              ! Көмекші етістік
%<adv%>       ! Adverb                     ! Үстеу
%<post%>      ! Postposition               ! Септеулік шылау
%<postadv%>   ! Postadverb                 ! "Постүстеу" (*1)
%<cnjcoo%>    ! Co-ordinating conjunction  ! Cалаластырғыш жалғаулық
%<cnjsub%>    ! Sub-ordinating conjunction ! Cабақтастырғыш жалғаулық
%<cnjadv%>    ! Adverbial conjunction      ! "Үстеу-жалғаулық"
%<ij%>        ! Interjection               ! Одағай
%<abbr%>      ! Abbreviation               ! Қысқарған сөз
%<cop%>       ! Copula                     ! Копула
%<ideo%>      ! Ideophone                  ! Еліктеу сөз
%<paren%>     ! Parentheses                ! Қыстырма сөз

! *1. Анықтаған сөздерінің артынан келетін үстеулер ("демеуліктер") !# ғана, -ақ

! Proper noun types
%<top%>       ! Toponym      ! Топоним
%<ant%>       ! Anthroponym  ! Кісі есімі
%<cog%>       ! Cognomen     ! Фамилия
%<pat%>       ! Patronym     ! Әке текті есім
%<org%>       ! Organisation ! Ұйым есімі
%<al%>        ! Other        ! Басқалар

!! gender of anthroponyms and cognoms
%<m%>         ! Masculine
%<f%>         ! Feminine
%<mf%>        ! Masculine/feminine !# basically cognoms without -ов/-ова,
                                   ! -ин/-ина endings

! "Syntactic" tags. Attributive use of non-adjectives etc.
%<attr%>      ! Attributive
%<subst%>     ! Substantive
%<advl%>      ! Adverbial

! Number
%<sg%>        ! Singular ! Жекеше
%<pl%>        ! Plural

! Possessives
%<px1sg%>     ! First person singular
%<px2sg%>     ! Second person singular
%<px3sp%>     ! Third person singular/plural
%<px1pl%>     ! First person plural
%<px2pl%>     ! Second person plural
%<px3pl%>     ! Third person plural (for reflexive)
!%<px%>        ! General possessive          !# -нікі  ! now gen.subst

! Cases
%<nom%>       ! Nominative
%<gen%>       ! Genitive
%<dat%>       ! Dative
%<acc%>       ! Accusative
%<abl%>       ! Ablative
%<loc%>       ! Locative
%<ins%>       ! Instrumental

!! some additional ~cases
%<sim%>       ! Similative
              !# DAй
%<abe%>       ! Abessive=Privative ! Лишительный
              !# SIZ (not used after posessives and cases)
%<reas%>      ! not used rigth now, just in case for
              !# LIKTAN
%<equ%>       ! ш{A} 

! Levels of comparison of adjectives
%<comp%>      ! Comparative

! TODO
!! Some adjecives have a "weakening" form, e.g. яшел>яшькелт, сары>саргылт; but
!! also ак>аксыл, кара>карасу. Sometimes they take the comparative RAK affix,
!! e.g. кызгылтрак.

! TODO
!! There is also what can be called a synthetic superlative form (as opposed
!! to the /ең<adv>+X<adj>/ construction) -- кап-кара, тип-тигез, төп-төгәл etc.
!! Probably easiest way would be just to lexicalize them, but make sure that
!! they don't take the comparative affix.

! Pronoun types
%<pers%>      ! Personal
%<recip%>     ! Reciprocal

!! Pronoun&Determiner types
%<dem%>       ! Demonstrative
%<ind%>       ! Indefinite
%<itg%>       ! Interrogative
%<qnt%>       ! Quantifier
%<neg%>       ! Negative       !# ешкім
              ! (NOTE: also used to denote negation in verbs, i.e for м{A})
%<ref%>       ! Reflexive

! Numeral types
%<ord%>       ! Ordinal
%<coll%>      ! Collective
%<dist%>      ! Distributive

! Verbal features

!! Mood
%<imp%>       ! Imperative
%<opt%>       ! Optative/jussive
%<evid%>      ! Evidential, a.k.a. "indirect" / non-eyewitness / hearsay

!! Derivation
%<caus%>      ! Causative
%<pass%>      ! Passive
%<coop%>      ! Cooperative

!! Tense / finite forms
%<pres%>      ! for "жатыр", "тұр", "отыр" and "жүр" ""
%<aor%>       ! -{E}
%<past%>      ! -{G}{A}н
%<ifi%>       ! -{D}{I}
%<ifi_evid%>  ! -{I}пт{I}
%<fut%>       ! -{I}р
%<fut_plan%>  ! -{M}{A}{K}
%<pih%>       ! -{E}тін       ! ~past imperfect ~"used to"

!! Non-Finite verb forms

!!! Participles
%<prc_perf%>  ! Perfect participle
              ! -{I}п
              !# "Бірақ мысығы үйде, _ұйықтап_ жатыр.";
%<prc_impf%>  ! Imperfect participle
              ! -{E}
              !# "...олар далада _ойнай_ алмады...";
%<prc_vol%>   ! Volition participle
              ! -{G}{I}
              !# барғым келмейді;
%<prc_cond%>  ! Conditional participle
              ! -с{A}
              !# ...жесең болады...;
%<prc_fplan%> ! Future plan participle
              ! -{M}{A}{K},
              ! -{M}{A}{K}ш{I} Dir/LR
              !# "Шал қазға бармақшы болады.";
%<prc_plan%>  ! Plan participle
              ! -{G}{A}л{I}
              !# мен сөйлескелі келдім;
!%<prc_irre%> ! Irrealis participle
              ! -{E}{T}{I}н
              ! (This form is analyzed as <gpr_impf> and <ger_impf>)
              ! (FIXME this might be wrong)

!!! Verbal adverbs ! Көсемшелер !Глагольные наречия
%<gna_perf%>  ! -{I}п
              !# "...ул вакытта инде кояш _баеп_, йолдызлар күренә башлаган
              !# иде..." (Ф.Хөсни);
%<gna_impf%>  ! -{A}	      
%<gna_cond%>  ! -с{A}
              !# ...қайда екенін _білсе_, маған бұл туралы айтыр еді...;
%<gna_until%> ! -{G}{A}нш{A}
              !# "Мен кеткенше ол жауап бермейді.";
%<gna_after%> ! -{G}{A}л{I}
              ! (NOTE: ambiguous with prc_plan)
              !# "Сабақ _біткелі_ екі сағат өтті."; Ол кеткелі біз жұмыс
              !# істемейдік;

!!! Verbal adjectives ! Есімшелер ! Глагольные причастия
%<gpr_past%>  ! -{G}{A}н               ! past verbal adjective
              !# келген адам; оқылмаған кітап;
%<gpr_impf%>  ! -{E}{T}{I}н            ! imperfect verbal adjective
              !# басқаратын = руководящий
%<gpr_pot%>   ! -{U}ш{I}               ! potential verbal adjective
              !# "...өзінің қадір-қасиетін арттыруға _тырысушы_ саясаткер...";
%<gpr_ppot%>  ! -{A}рл{I}{K}
              !# сүйсінерлік ерлік;
%<gpr_fut%>   ! -{I}р                  ! future verbal adjective
              !# ""Барар жерің Балкан тау, ол да біздің көрген тау";

!!! Gerunds (verbal nouns)
%<ger%>       ! -{U}
%<ger_past%>  ! -{G}{A}н
%<ger_perf%>  ! -{G}{A}нл{I}{K}   (stresses the fact that something happened)
%<ger_ppot%>  ! -{A}рл{I}{K}      (~the ability to do the denoted action)
              !# "_Сүйсінерлігі_ сол, өз сеніміне берік те адал халқымыз дінін
              !# сатпады, өзгенің тәтті де сылдыр сөзіне ермеді...";
%<ger_abs%>   ! -уш{I}л{I}{K}          FIXME CHECK
%<ger_fut%>   ! -{A}р
              !# "Мен айтарымды айттым";
%<ger_impf%>  ! -{E}{T}{I}н
              !# "Ол бүгін кешкілік болатынын айтты";
%<abs%>       ! -LIK as e.g. seen in the -{E}{T}{I}нд{I}{K} form
%<ger_obs%>   ! -{М}{А}{К}         ("obsolete": used only in archaic registers)
              !# "Адам өлмек үшін туған"

!! Transitivity
%<tv%>        ! Transitive
%<iv%>        ! Intransitive 

! Person
%<p1%>        ! First person
%<p2%>        ! Second person
%<p3%>        ! Third person
%<frm%>       ! Formality

! Modal particles
%<qst%>       ! Modal question particle
              !# м{A}
%<emph%>      ! Emphasizing modal particle
              !# -шы/-ші
%<mod_ass%>   ! Assertive modal particle
              !# ғой/қой
%<mod_emo%>   ! Emotative modal particles
              !# -ай, -ау
%<mod%>       ! Other modal words (шығар, сияқты etc)

%<unk%>       ! For foreign tokens

%<err_orth%>  ! For orthographic errors

! Punctuation
%<percent%>   ! Percent
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)
%<ltr%>       ! Letter

! Archiphonemes and escaped symbols

%{L%}         ! Archiphoneme 'l': realised as л, д, т
%{N%}         ! Archiphoneme 'n': realised as н, д, т
%{M%}         ! Archiphoneme 'm': realised as м, б, п
%{G%}         ! Archiphoneme 'g': realised as к, қ, г, ғ
%{G%}         ! Archiphoneme 'k': realised as к, қ  >>>FIXME ???<<<
%{D%}         ! Archiphoneme 'd': realised as д, т
%{A%}         ! Archiphoneme 'a': realsied as е, а
%{I%}         ! Archiphoneme 'i': realised as і, ы
%{S%}         ! Realised as 'с' or ''; only used in 3rd person morphology
%{K%}         ! Realised as 'к' or 'қ'
%{n%}         ! Realised as 'н' or '';
              ! only used in 3rd person morphology and -NIKI
%{l%}         ! Realised as 'л' or 'н'; only used in passive -{I}{l}
%{y%}         ! Realised as '' or '{I}'; only used in epenthesis for nouns
%{o%}         ! Realised as ''; triggers dialectal interpretation of Iп
%{E%}         ! Realised as 'а', 'е', 'й' (={A}/й)
%{д%}         ! Realised as д before vowels, 0 otherwise
%{т%}         ! Realised as т before vowels, 0 otherwise


%{а%}         ! Archiphoneme for back vowel numerals/abbreviation
%{э%}         ! Archiphoneme for front vowel numerals/abbreviation
%{ә%}         ! Surfaces as ә, but:
              ! triggers front harmony for {I} and back harmony for {A}

%{й%}         !
%{л%}         !
%{н%}         !
%{з%}         !
%{т%}         !
%{с%}         !

%{☭%}         ! Used to tell the phonology about a Russian word
%{і%}         !

%{❗%}         ! Obstacles on the road!!!
%{ъ%}         ! Force back harmony
%{ь%}         ! Force front harmony

%>            ! Morpheme boundary
%,            !
%.            !
%             ! Space
%-            !
%%            !

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!==============================================================================!
!
!                               N U M E R A L S
!
!    WARNING: This section should not be modified without consultation.
!             Test cases are in dev/numerals.
!==============================================================================!

LEXICON CASE-NONOM

! This continuation lexicon is a copy of the normal case lexicon,
! only it does not include nominative
!   1<num><nom> = 1, 1<num><acc> = 1-{N}{I}

%<gen%>:%>%{N%}%{I%}ң CLITICS-NO-COP ;
%<dat%>:%>%{G%}%{A%} CLITICS-NO-COP ;
%<acc%>:%>%{N%}%{I%} CLITICS-NO-COP ;
%<abl%>:%>%{D%}%{A%}н # ;
%<loc%>:%>%{D%}%{A%} # ;
%<ins%>:%>%{M%}ен CLITICS-NO-COP ;
%<ins%>:%>%{M%}енен CLITICS-NO-COP ; ! Dir/LR  ! It should be analysed like this, but generated as мен/бен/пен

LEXICON CASE-ETC-NONOM

CASE-NONOM ;
GENERAL-POSSESSIVE-ETC ;

LEXICON FULL-NOMINAL-INFLECTION-NONOM

POSSESSIVES ;
CASE-ETC-NONOM ;

LEXICON SUBST-NONOM

FULL-NOMINAL-INFLECTION-NONOM ;
%<pl%>:%>%{L%}%{A%}р FULL-NOMINAL-INFLECTION-NONOM ;

LEXICON SQRD
! This lexicon includes common ways of writing squared, cubed etc.

²%<num%>:2 # ;  ! Dir/LR
³%<num%>:3 # ;  ! Dir/LR
²%<num%>:² # ;
³%<num%>:³ # ;
%<num%>: # ;

LEXICON NUM-DIGIT

%<num%>: # ;
%<num%>%<subst%>%<nom%>: # ;
%<num%>%<subst%>:%- SUBST-NONOM ;
%<num%>%<subst%>: SUBST-NONOM ; ! Dir/LR

%<num%>%<ord%>:%-%{I%}нш%{I%} # ;
%<num%>%<ord%>%<subst%>%<nom%>:%-%{I%}нш%{I%} # ;
%<num%>%<ord%>%<subst%>:%-%{I%}нш%{I%} SUBST-NONOM ;
%<num%>%<ord%>:%-ш%{I%} # ; ! Dir/LR
%<num%>%<ord%>%<subst%>%<nom%>:%-ш%{I%} # ; ! Dir/LR
%<num%>%<ord%>%<subst%>:%-ш%{I%} SUBST-NONOM ; ! Dir/LR

%<num%>%<coll%>%<advl%>:%-%{A%}у # ;
%<num%>%<coll%>%<subst%>%<nom%>:%-%{A%}у # ;
%<num%>%<coll%>%<subst%>:%-%{A%}у SUBST-NONOM ;

%<num%>%<percent%>%<nom%>:%% # ;

%<num%>%<percent%>:%%%{а%}%{з%}%- SUBST-NONOM ;
%<num%>%<percent%>:%%%{а%}%{з%} SUBST-NONOM ; ! Dir/LR
%<num%>%<percent%>:%%%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR
%<num%>%<percent%>:%%%{э%}%{с%} SUBST-NONOM ; ! Dir/LR

°С%<num%>:°С # ;
°С%<num%>%<subst%>%<nom%>:°С # ;
°С%<num%>%<subst%>:°С%{а%}%{с%}%- SUBST-NONOM ;
°С%<num%>%<subst%>:°С%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>:ºС # ; ! Dir/LR
°С%<num%>%<subst%>%<nom%>:ºС # ; ! Dir/LR
°С%<num%>%<subst%>:ºС%{а%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>%<subst%>:ºС%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>:С # ; ! Dir/LR
°С%<num%>%<subst%>%<nom%>:С # ; ! Dir/LR
°С%<num%>%<subst%>:С%{а%}%{с%}%- SUBST-NONOM ; ! Dir/LR
°С%<num%>%<subst%>:С%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR

С%<num%>:° # ; ! Dir/LR
С%<num%>%<subst%>%<nom%>:° # ; ! Dir/LR
С%<num%>%<subst%>:°%{а%}%{с%}%- SUBST-NONOM ; ! Dir/LR
С%<num%>%<subst%>:°%{э%}%{с%}%- SUBST-NONOM ; ! Dir/LR

! Measurement units

км:км SQRD ;
мм:мм SQRD ;
см:см SQRD ;
м:м SQRD ;

LEXICON LASTDIGIT

1:1%{э%}%{й%}           NUM-DIGIT ; ! "бір"
2:2%{э%}                NUM-DIGIT ; ! "екі"
3:3%{э%}%{с%}           NUM-DIGIT ; ! "үш"
4:4%{э%}%{с%}           NUM-DIGIT ; ! "төрт"
5:5%{э%}%{с%}           NUM-DIGIT ; ! "бес"
6:6%{а%}                NUM-DIGIT ; ! "алты"
7:7%{э%}                NUM-DIGIT ; ! "жеті"
8:8%{э%}%{з%}           NUM-DIGIT ; ! "сегіз"
9:9%{а%}%{з%}           NUM-DIGIT ; ! "тоғыз"

LEXICON LASTDIGIT-REST

1%0:1%0%{а%}%{н%}       NUM-DIGIT ; ! "он"
2%0:2%0%{а%}            NUM-DIGIT ; ! "қырық"
3%0:3%0%{а%}%{з%}       NUM-DIGIT ; ! "отыз"
4%0:4%0%{а%}%{с%}       NUM-DIGIT ; ! "қырық"
5%0:5%0%{э%}%{й%}       NUM-DIGIT ; ! "елу"
6%0:6%0%{а%}%{с%}       NUM-DIGIT ; ! "алпыс"
7%0:7%0%{э%}%{с%}       NUM-DIGIT ; ! "жетпіс"
8%0:8%0%{э%}%{н%}       NUM-DIGIT ; ! "сексен"
9%0:9%0%{а%}%{н%}       NUM-DIGIT ; ! "тоқсан"

LEXICON POWERS
%0%0:%0%0%{э%}%{з%}     NUM-DIGIT ; ! "жүз"
%0%0%0:%0%0%0%{а%}%{н%} NUM-DIGIT ; ! "мың"

LEXICON LOOP

%,:%, DIGITLEX ;
%.:%. DIGITLEX ;
%,:%, LASTDIGIT ;
%.:%. LASTDIGIT ;
%,:%, LASTDIGIT-REST ;
%.:%. LASTDIGIT-REST ;
%,:%, POWERS ;
%.:%. POWERS ;
      DIGITLEX ;
      LASTDIGIT ;
      LASTDIGIT-REST ;
      POWERS ;

LEXICON DIGITLEX

%0:%0 LOOP ;
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;

!==============================================================================!
!          N U M E R A L     L E X I C O N     E N D S     H E R E
!==============================================================================!

!==============================================================================!
! CLITICS
!==============================================================================!

! First, let's list all the clitics we have

LEXICON CLIT-EMPH

%+шы%<emph%>:%>ш%{I%} # ;
# ;

LEXICON CLIT-QST-MA

%+ма%<qst%>:%>% %{M%}%{A%} # ;

LEXICON CLIT-QST

CLIT-QST-MA ;

LEXICON CLIT-MODEMO

%+ау%<mod_emo%>:%-ау # ;
%+ай%<mod_emo%>:%-ай # ;

LEXICON CLIT-MODASS

%+ғой%<mod_ass%>:% %{G%}ой # ;
# ;

LEXICON CLIT-GHANA-ETC

%+%-ақ%<postadv%>:%-ақ # ; ! ""
%+ғана%<postadv%>:% %{G%}ана # ; ! ""
%+да%<cnjcoo%>:% %{D%}%{A%} # ; ! ""
%+да%<postadv%>:% %{D%}%{A%} # ; ! ""
%+шығар%<mod%>:% шығар # ; ! ""
# ;

LEXICON CLIT-COP

%+е%<cop%>%<aor%>: V-PERS-S1 ;
%+ма%<qst%>+е%<cop%>%<aor%>%<evid%>:% %{M%}екен V-PERS-S1 ;

! Now, let's group them depending on what they can attach to

LEXICON CLITICS-NO-COP

CLIT-QST ;
CLIT-MODASS ;
CLIT-MODEMO ;
CLIT-EMPH ;
CLIT-GHANA-ETC ;

LEXICON CLITICS-NO-COP-NO-QST

CLIT-MODASS ;
CLIT-MODEMO ;
CLIT-GHANA-ETC ;

LEXICON CLITICS-INCL-COP         ! Can appear only after nominals
                                 ! (and only after some cases).
CLITICS-NO-COP ;
CLIT-COP ;

!==============================================================================!
!                         NOMINAL INFLECTION
! This section contains everything needed to describe inflection of nominals.
! It starts with the LEXICON FULL-NOMINAL-INFLECTION
!==============================================================================!

!-------------------------------------------------------------------------------
! The following combinations are possible (examples in Tatar):
!
! stem(plural) > case                                     # әти<nom>, әтине<acc>
!----
! stem(plural) > possessives > case                       !# әтиемне<px1sg><acc>
! stem(plural) > possessives > ныкы/дагы/дай(*1)
                                               !# әтиемнеке,китабындагы,әтиемдәй
! stem(plural) > possessives > ныкы > case
! stem(plural) > possessives > дагы(plural(*2)) > case
                                   !# Монда _эчеңдәгене_ тышка чыгарырга ярамый.
! stem(plural) > possessives > дагы(plural) > possessives > case
                     !# _Эчендәгесе_(*3) – тышында, диләр андый кешеләр турында.
!----
! stem(plural) > ныкы/дагы/дай                         !# әтинеке/әтидәге/әтидәй
! stem(plural) > ныкы/дагы > case  !#Бу соңгы _еллардагының_ өчтән икесе чамасы.
!-----
! stem(plural) > сыз
! stem > сыз(plural) > case
! stem > сыз(plural) > possessives > case
!----
! *1. "ныкы" form in some Tatar grammars is called "predicative genitive".
!     Indeed, when not substantivized, it is used as a predicate:
!     "Бу яулык әнинеке." (vs "Әнинең яулыгы.)
!
!     Currently we don't mark it with a <subst> tag, but probably we should,
!     since we let it continue with case tags.
!
!     An alternative to a [predicative general possessive <px>] vs
!     [substantivized general possessive <px>subst>] opposition is to view it as
!     an always substantivized thing and hence mark it with a <subst> tag
!     (see e.g. http://wiki.apertium.org/wiki/Turkic_languages/Ki)
!     At the end of sentences it will have attached copulas, which will indicate
!     predicative use anyway.
! *2. TODO: Does double plural in a word make sence? Ерак илләрдәгеләр looks ok,
!     and occurs in the RFERL copus (though very rarely). Plural affix both
!     before and after the the general possessive does not look ok.
! *3. This doesn't work atm (it's recursive), but it doesn't seem to be very
!     frequent also.
!     Most likely it is restricted to px3sp.
!-------------------------------------------------------------------------------

!-------------------------------------------------------------------------------
! A note on abessive SIZ:
! It doesn't appear after possessives.
! We don't let it take copula suffixes here. A counterexample could be e.g.
! "жұмыс-сыз-бын", but that's only because "жұмыссыз" has lexicalized as an
! adjective (try to grep "с[ыі]з[мбп][ыі]н" on a large corpora -- that's just a
! few words which are rather  adjectives.
!-------------------------------------------------------------------------------

LEXICON CASES-COMMON

%<gen%>:%>%{N%}%{I%}ң CLITICS-NO-COP ;
%<dat%>:%>%{G%}%{A%} CLITICS-NO-COP ;
%<acc%>:%>%{N%}%{I%} CLITICS-NO-COP ;
%<ins%>:%>%{M%}ен CLITICS-NO-COP ;
%<ins%>:%>%{M%}енен CLITICS-NO-COP ; ! Dir/LR

LEXICON CASES

CASES-COMMON ;
%<nom%>: CLITICS-INCL-COP ;
%<abl%>:%>%{D%}%{A%}н CLITICS-INCL-COP ;
%<loc%>:%>%{D%}%{A%} CLITICS-INCL-COP ;

!! LEXICON CASES-WITHOUT-COPULAS
!! 
!! CASES-COMMON ;
!! %<nom%>: CLITICS-NO-COP ;
!! %<abl%>:%>%{D%}%{A%}н CLITICS-NO-COP ;
!! %<loc%>:%>%{D%}%{A%} CLITICS-NO-COP ;

LEXICON ATTR-SUBST

%<attr%>: # ;
%<subst%>: CASES ;
%<subst%>: POSSESSIVES ;
%<subst%>%<pl%>:%>%{L%}%{A%}р CASES ;
%<subst%>%<pl%>:%>%{L%}%{A%}р POSSESSIVES ;

%<attr%>: GENERAL-POSSESSIVE-ETC ;

LEXICON GENERAL-POSSESSIVE-ETC

!------------------------------------------------------------------------------!
! <n.pl.нікі.pl> sequence should not be allowed. This form is rare (1 (!)
! match in RFERL corpus for 'нікілар'), probably not worh bothering at all.
! It could be handled in a way abessive is handled, but since NIKI in contrast
! to the abessive SIZ can appear after possessves, that would require
! duplicating possessives as well.
!------------------------------------------------------------------------------!
! try нікілер?  -JNW

!gen.subst part was:
!%<px%>:%>%{N%}ікі%{n%} CASES ;
!%<px%>%<pl%>:%>%{N%}ікі%>л%{A%}р CASES ;

%<gen%>%<subst%>:%>%{N%}ікі%{n%} CASES ;
%<gen%>%<subst%>%<pl%>:%>%{N%}ікі%>л%{A%}р CASES ;
%<loc%>:%>%{D%}%{A%}%{G%}%{I%} ATTR-SUBST ;
%<sim%>:%>%{D%}%{A%}й CLITICS-INCL-COP ;
%<sim%>:%>%{D%}%{A%}й POSSESSIVES ;
%<sim%>:%>%{D%}%{A%}й CASES ;
%<sim%>:%>%{D%}%{A%}й PLURAL ;
%<equ%>:%>ш%{A%} CLITICS-INCL-COP ;

LEXICON CASES-ETC

CASES ;
GENERAL-POSSESSIVE-ETC ;

LEXICON POSSESSIVES

%<px1sg%>:%>%{I%}м CASES-ETC ;
%<px2sg%>:%>%{I%}ң CASES-ETC ;
%<px3sp%>:%>%{S%}%{I%}%{n%} CASES-ETC ;
%<px1pl%>:%>%{I%}м%{I%}з CASES-ETC ;
%<px2pl%>:%>%{I%}ңд%{A%}р CASES-ETC ;

%<px2sg%>%<frm%>:%>%{I%}ң%{I%}з CASES-ETC ;
%<px2pl%>%<frm%>:%{I%}ң%{I%}зд%{A%}р CASES-ETC ;

LEXICON ABESSIVE-POSTPOSITION

%+сыз%<post%>:%>с%{I%}з CLITICS-NO-COP ;

LEXICON LI-POSTPOSITION

%+лы%<post%>:%>%{L%}%{I%} CLITICS-NO-COP ;

LEXICON PLURAL

%<pl%>:%>%{L%}%{A%}р CASES-ETC ;
%<pl%>:%>%{L%}%{A%}р POSSESSIVES ;
%<pl%>:%>%{L%}%{A%}р ABESSIVE-POSTPOSITION ;

LEXICON FULL-NOMINAL-INFLECTION

PLURAL ;
CASES-ETC ;
POSSESSIVES ;
ABESSIVE-POSTPOSITION ;
LI-POSTPOSITION ;

!!!!!!!!!!!!!!!!!!!!!!     ADJECTIVE'S (ADJECTIVIAL) INFLECTION (ADJ-COMMON)

! FIXME: this section might need some refactoring
!! i.e. change <adj><comp><subst> to <adj><subst><comp>
!! and make use of ADJ-LEVELS class

!! LEXICON COMPARATIVE
!! 
!! %<comp%>:%>%{I%}р%{A%}%{K%} CLITICS-NO-COP ;
!! CLITICS-NO-COP ;

LEXICON A1                ! adjectives that can be both substantivised and andverbialised;
                          ! all three readings (<adj>, <adj.subst> and <adj.advl>) have comparison levels.
                          !# жақсы, тез

%<adj%>: CLITICS-INCL-COP ;                             !# жақсы адам
%<adj%>%<comp%>:%>%{I%}р%{A%}%{K%} CLITICS-INCL-COP ;   !# жақсырақ іс
%<adj%>%<comp%>:%>%{L%}%{A%}у CLITICS-INCL-COP ;        !# жақсырақ іс Dir/LR

%<adj%>%<subst%>: FULL-NOMINAL-INFLECTION ;                             !# жақсыны таптым
%<adj%>%<comp%>%<subst%>:%>%{I%}р%{A%}%{K%} FULL-NOMINAL-INFLECTION ;   !# жақсырақты таптым
%<adj%>%<comp%>%<subst%>:%>%{L%}%{A%}у FULL-NOMINAL-INFLECTION ;        !# жақсырақты таптым Dir/LR
%<adj%>%<comp%>%<advl%>:%>%{L%}%{A%}у CLITICS-NO-COP ;                  !# "...ион көп болса соғұрлым тоқ жақсырақ өтеді." Dir/LR

%<adj%>%<advl%>: CLITICS-NO-COP ;                               !# жақсы білемін
%<adj%>%<comp%>%<advl%>:%>%{I%}р%{A%}%{K%} CLITICS-NO-COP ;     !# "...ион көп болса соғұрлым тоқ жақсырақ өтеді."

LEXICON A2                 ! (derived/not fully lexicalised) adjectives without adverbial reading;
                           ! <adj> and <adj.subst> readings have comparison levels.
                           !# ескі

%<adj%>: CLITICS-INCL-COP ;                             !# ескі дос
%<adj%>%<comp%>:%>%{I%}р%{A%}%{K%} CLITICS-INCL-COP ;   !# ескірек заманда
%<adj%>%<comp%>:%>%{L%}%{A%}у CLITICS-INCL-COP ;        !# ескірек заманда Dir/LR

%<adj%>%<subst%>: FULL-NOMINAL-INFLECTION ;                             !# "Ол ескіні ертеден есінде сақтаған, жаңаның жалынды жаршысы болған ақын."
%<adj%>%<comp%>%<subst%>:%>%{I%}р%{A%}%{K%} FULL-NOMINAL-INFLECTION ;   !# "Ихтимал, бу бина башкасы, искерәге урынында утырадыр."
%<adj%>%<comp%>%<subst%>:%>%{L%}%{A%}у FULL-NOMINAL-INFLECTION ;        !# "Ихтимал, бу бина башкасы, искерәге урынында утырадыр." Dir/LR

LEXICON A3                 ! (derived/not fully lexicalised) adjectives without adverbial reading,
                           ! so-called "predicatives" (бар, жоқ);
                           ! no comparison levels at all.
                           !# көктемгі, басты, бар, жоқ

%<adj%>: CLITICS-INCL-COP ;         !# көктемгі су тасқыны
%<adj%>%<subst%>: FULL-NOMINAL-INFLECTION ;           !# "...Самаранч ең бастыны түсінді..."
                                    !# "Аты бардың заты бар."

! FIXME:NOTE:loc.attr/subst form of some of them seems to be pure overgeneration:
! e.g. *язгыдагы

LEXICON A4                 ! "pure" adjectives - no adverbial and substantive readings, no comparison levels;
                           !# ұлттық

%<adj%>: CLITICS-INCL-COP ;   !# ұлттық тағам

LEXICON A6                 ! (derived/not fully lexicalised) adjectives without comparative reading,
                           ! no comparison levels at all.
                           !# examples go here

%<adj%>: CLITICS-INCL-COP ;                           !# example 1
%<adj%>%<subst%>: FULL-NOMINAL-INFLECTION ;           !# example 2
%<adj%>%<advl%>: CLITICS-NO-COP ;                     !# example 3

LEXICON V-ADJ

# ;
%<subst%>: GER-INFL ; ! Don't be confused by the name of the cont.class,
                      ! it's basically just a simplified FULL-NOMINAL-INFLECTION

!!!!!!!!!!!!!!!!!!!!!!     VERBAL INFLECTION (V-INFL-COMMON)

LEXICON V-PERS-S1-NO3PERSON

! Pointed to by: V-PERS-S1 and V-PERS-AOR
! Irregular p1.sg form is in V-PERS-AOR

%<p1%>%<sg%>:%>%{M%}%{I%}н CLITICS-NO-COP ;
%<p2%>%<sg%>:%>с%{I%}ң CLITICS-NO-COP ;
%<p1%>%<pl%>:%>%{M%}%{I%}з CLITICS-NO-COP ;
%<p2%>%<pl%>:%>с%{I%}ңд%{A%}р CLITICS-NO-COP ;

%<p2%>%<frm%>%<sg%>:%>с%{I%}з CLITICS-NO-COP ;
%<p2%>%<frm%>%<pl%>:%>с%{I%}зд%{A%}р CLITICS-NO-COP ;

LEXICON V-PERS-S1

! Used for: -AR-<fut>, -GAn-<past>, -MAK-<fut_plan>, copula

V-PERS-S1-NO3PERSON ;
%<p3%>%<sg%>: CLITICS-NO-COP ;
%<p3%>%<pl%>: CLITICS-NO-COP ;

LEXICON V-PERS-S2

! Used for: -DI-<ifi>, -sA-<prc_cond>, -sA-<gna_cond>

%<p1%>%<sg%>:%>м CLITICS-NO-COP ;
%<p2%>%<sg%>:%>ң CLITICS-NO-COP ;
%<p3%>%<sg%>: CLITICS-NO-COP ;
%<p1%>%<pl%>:%>%{K%} CLITICS-NO-COP ;
%<p2%>%<pl%>:%>ңд%{A%}р CLITICS-NO-COP ;
%<p3%>%<pl%>: CLITICS-NO-COP ;

%<p2%>%<frm%>%<sg%>:%>ң%{I%}з CLITICS-NO-COP ;
%<p2%>%<frm%>%<pl%>:%>ң%{I%}зд%{A%}р CLITICS-NO-COP ;

LEXICON V-PERS-AOR

! As the name indicates, used for -{E}-<aor>
! NOTE that: Алма театрға бара<aor><p3><sg> ма? – Алма в театр идёт?

V-PERS-S1-NO3PERSON ;

%<p1%>%<sg%>:%>м CLITICS-NO-COP ;                ! Dir/LR

%<p3%>%<sg%>:%>д%{I%} CLITICS-NO-COP-NO-QST ;
%<p3%>%<sg%>:%>д%{I%} CLIT-QST ;              ! Dir/LR
%<p3%>%<sg%>: CLIT-QST ;

%<p3%>%<pl%>:%>д%{I%} CLITICS-NO-COP-NO-QST ;
%<p3%>%<pl%>:%>д%{I%} CLIT-QST ;              ! Dir/LR
%<p3%>%<pl%>: CLIT-QST ;

LEXICON V-PERS-IFI_EVID

V-PERS-S1-NO3PERSON ;
%<p3%>%<sg%>:%>т%{I%} CLITICS-NO-COP ;
%<p3%>%<pl%>:%>т%{I%} CLITICS-NO-COP ;

LEXICON VOL-ENDINGS

%<p1%>%<sg%>:%>м CLIT-GHANA-ETC ;
%<p2%>%<sg%>:%>ң CLIT-GHANA-ETC ;
%<p3%>%<sg%>:%>с%{I%} CLIT-GHANA-ETC ;
%<p1%>%<pl%>:%>м%{I%}з CLIT-GHANA-ETC ;
%<p2%>%<pl%>:%>л%{A%}р%>%{I%}ң CLIT-GHANA-ETC ;
%<p3%>%<pl%>:%>с%{I%} CLIT-GHANA-ETC ;
%<p3%>%<pl%>:%>л%{A%}р%>%{I%} CLIT-GHANA-ETC ;             ! Dir/LR

%<p2%>%<frm%>%<sg%>:%>ң%{I%}з CLIT-GHANA-ETC ;
%<p2%>%<frm%>%<pl%>:%>л%{A%}р%>%{I%}ң%{I%}з CLIT-GHANA-ETC ;

!!!!!!!   G E R U N D S   I N F L E C T I O N

! This is a somewhat simplified version of FULL-NOMINAL-INFLECTION.
! The idea was to avoid overgeneration (I don't think that ger+DAGI would take
! case endings e.g. Same for ger+NIKI). Still, this cont.class might be not
! suitable for all of the gerunds. /IS/

LEXICON GER-CASE-NO_COPULA

CASES-COMMON ;
%<nom%>: CLITICS-NO-COP ;
%<abl%>:%>%{D%}%{A%}н CLITICS-NO-COP ;
%<loc%>:%>%{D%}%{A%} CLITICS-NO-COP ;

LEXICON GER-ATTR/SUBST

%<attr%>: CLITICS-NO-COP ;
!%<subst%>: CASES ;
!%<subst%>%<pl%>:%>%{L%}%{A%}р CASES ;

!! LEXICON GER-ABE-ATTR/ADVL/SUBST
!! 
!! !%<attr%>: CLITICS-NO-COP ;
!! %<advl%>: CLITICS-NO-COP ;
!! !%<subst%>: CASE-2 ;
!! !%<subst%>%<pl%>:%>%{L%}%{A%}р CASE-2 ;

LEXICON GER-ABE-ETC          ! Stuff which doesn't appear after possessives

%+сыз%<post%>:%>с%{I%}з CLITICS-NO-COP ; ! 2014-08-16//FMT: this was "GER-ABE-ATTR/ADVL/SUBST"

LEXICON GER-GENERAL-POSSESSIVE-ETC      ! Stuff which can appear after possessives too

%<gen%>%<subst%>%<nom%>:%>%{N%}ікі%{n%} CLITICS-NO-COP ; ! 2015-01-18//JNW: added <nom>, since it needs a case for transfer, and no other cases are ever added, or even presumed by the transducer to be okay.  But worth CHECK ing..
%<loc%>:%>%{D%}%{A%}%{G%}%{I%} GER-ATTR/SUBST ;
%<sim%>:%>%{D%}%{A%}й CLITICS-NO-COP ;

LEXICON GER-CASE-ETC

GER-CASE-NO_COPULA ;
GER-GENERAL-POSSESSIVE-ETC ;

LEXICON GER-POSSESSIVES

%<px1sg%>:%>%{I%}м GER-CASE-ETC ;
%<px2sg%>:%>%{I%}ң GER-CASE-ETC ;
%<px3sp%>:%>%{S%}%{I%}%{n%} GER-CASE-ETC ;
%<px1pl%>:%>%{I%}м%{I%}з GER-CASE-ETC ;
%<px2pl%>:%>%{I%}ңд%{A%}р GER-CASE-ETC ;

%<px2sg%>%<frm%>:%>%{I%}ң%{I%}з GER-CASE-ETC ;
%<px2pl%>%<frm%>:%>%{I%}ң%{I%}зд%{A%}р GER-CASE-ETC ;

LEXICON GER-INFL

GER-POSSESSIVES ;
GER-CASE-ETC ;
%<pl%>:%>%{L%}%{A%}р GER-POSSESSIVES ;
%<pl%>:%>%{L%}%{A%}р GER-CASE-ETC ;
GER-ABE-ETC ;

!!!!!!!   end of gerunds inflection lexicon

LEXICON V-FINITE-IRREGULAR_NEGATIVE

%<fut%>:%>%{A%}р V-PERS-S1 ;
%<neg%>%<fut%>:%>%{M%}%{A%}с V-PERS-S1 ;

%<fut_plan%>:%>%{M%}%{A%}%{K%} V-PERS-S1 ;
%<fut_plan%>:%>%{M%}%{A%}%{K%}%>ш%{I%} V-PERS-S1 ; ! Dir/LR "...Менің қошақанымды қайда алып кетпекшісің?... (Экзюперидан)"
%<neg%>%<fut_plan%>:%>%{M%}%{A%}%{K%}% емес V-PERS-S1 ;

%<past%>:%>%{G%}%{A%}н V-PERS-S1 ;
%<neg%>%<past%>:%>%{G%}%{A%}н% емес V-PERS-S1 ;  ! more colloquial than regular negative

%<ifi%>:%>%{D%}%{I%} V-PERS-S2 ;
%<neg%>%<ifi%>:%>%{G%}%{A%}н% жоқ V-PERS-S1 ;    ! more colloquial than regular negative


LEXICON V-FINITE-REGULAR_NEGATIVE

%<aor%>:%>%{E%} V-PERS-AOR ;
%<past%>:%>%{G%}%{A%}н V-PERS-S1 ;      ! Dir/LR - see irregular forms above
%<ifi%>:%>%{D%}%{I%} V-PERS-S2 ;        ! Dir/LR - see irregular forms above
%<pih%>:%>%{E%}т%{I%}н V-PERS-S1 ;

%<aor%>%<evid%>:%>%{E%}д%{I%}% екен V-PERS-S1 ;    ! барады екенмін / бармайды екенмін
%<past%>%<evid%>:%>%{G%}%{A%}н% екен V-PERS-S1 ;   ! барған екенмін / бармаған екенмін
%<ifi%>%<evid%>:%>%{I%}п V-PERS-IFI_EVID ;         ! барыппын / бармаппын
%<ifi%>%<evid%>:%>%{o%}%{I%}п V-PERS-IFI_EVID ;    ! бопты   ! Dir/LR
%<neg%>%<ifi%>%<evid%>:%>%{G%}%{A%}н% жоқ% екен V-PERS-S1 ; ! барған жоқ екенмін

LEXICON V-NONFINITE-IRREGULAR_NEGATIVE

! Participles
%<prc_perf%>:%>%{I%}п CLIT-GHANA-ETC ;
%<prc_perf%>:%>%{o%}%{I%}п CLIT-GHANA-ETC ;            ! Dir/LR
       ! for things like боп, сап, кеп, қап, қып, etc.
%<neg%>%<prc_perf%>:%>%{M%}%{A%}%>%{E%} CLIT-GHANA-ETC ;
%<prc_vol%>:%>%{G%}%{I%} VOL-ENDINGS ;
%<prc_fplan%>:%>%{M%}%{A%}%{K%} CLITICS-NO-COP ;
%<prc_fplan%>:%>%{M%}%{A%}%{K%}ш%{I%} CLITICS-NO-COP ;           ! Dir/LR
%<prc_plan%>:%>%{G%}%{A%}л%{I%} CLIT-GHANA-ETC ;

! Verbal adverbs
%<gna_perf%>:%>%{I%}п CLITICS-NO-COP ;
%<neg%>%<gna_perf%>:%>%{M%}%{A%}%>%{E%} CLITICS-NO-COP ;
%<neg%>%<gna_perf%>:%>%{M%}%{A%}%>ст%{A%}н CLITICS-NO-COP ; ! Dir/LR
%<neg%>%<gna_perf%>:%>%{M%}%{A%}%>й%{I%}нш%{A%} CLITICS-NO-COP ;       ! Dir/LR
%<gna_until%>:%>%{G%}%{A%}нш%{A%} CLITICS-NO-COP ;

! Verbal Adjectives
%<gpr_fut%>:%>%{A%}р V-ADJ ;
%<neg%>%<gpr_fut%>:%>%{M%}%{A%}%>%с V-ADJ ;

%<gpr_ppot%>:%>%{A%}р%>л%{I%}%{K%} GER-INFL ;
%<neg%>%<gpr_ppot%>:%>%{M%}%{A%}%>ст%{I%}%{K%} GER-INFL ;

!%<neg%>%<gpr_pot2%>:%>%{G%}%{I%}с # ; FIXME looks wrong

! Gerunds
%<ger_fut%>:%>%{A%}р GER-POSSESSIVES ;  ! FIXME: check that e.g. no possession + accusative isn't valid (I think it is) -JNW
%<neg%>%<ger_fut%>:%>%{M%}%{A%}%>с GER-POSSESSIVES ;

%<ger_ppot%>:%>%{A%}р%>л%{I%}%{K%} GER-POSSESSIVES ;
%<neg%>%<ger_ppot%>:%>%{M%}%{A%}%>ст%{I%}%{K%} GER-POSSESSIVES ;

%<ger_obs%>:%>%{M%}%{A%}%{K%} GER-INFL ;
! FIXME <ger_obs>:
! - does it have a negative form?  if regular, with -МА,
!   put in the REGULAR_NEGATIVE category
! - does it take possessive forms?  if not, change
!   continuation lexicon to something other than GER-INFL

LEXICON V-NONFINITE-REGULAR_NEGATIVE

! Participles
%<prc_impf%>:%>%{E%} CLIT-GHANA-ETC ;
%<prc_cond%>:%>с%{A%} V-PERS-S2 ;

! Verbal adverbs
%<gna_cond%>:%>с%{A%} V-PERS-S2 ;
%<gna_impf%>:%>%{E%} CLIT-GHANA-ETC ;
%<gna_after%>:%>%{G%}%{A%}л%{I%} CLITICS-NO-COP ;

! Verbal adjectives
%<gpr_past%>:%>%{G%}%{A%}н V-ADJ ;
%<gpr_impf%>:%>%{E%}т%{I%}н V-ADJ ;
%<gpr_pot%>:%>у%>ш%{I%} V-ADJ ;

! Gerunds
%<ger%>:%>у GER-INFL ;
%<ger_past%>:%>%{G%}%{A%}н GER-INFL ;
%<ger_perf%>:%>%{G%}%{A%}н%>%{L%}%{I%}%{K%} GER-INFL ;
%<ger_abs%>:%>у%>ш%{I%}%>%{L%}%{I%}%{K%} GER-INFL ;
%<ger_impf%>:%>%{E%}т%{I%}н GER-POSSESSIVES ;
%<ger_impf%>%<abs%>:%>%{E%}т%{I%}н%>д%{I%}%{K%} GER-POSSESSIVES ;

! Imperatives
%<imp%>%<p2%>%<sg%>: CLIT-EMPH ;
%<imp%>%<p2%>%<pl%>:%>%{I%}ң%>д%{A%}р CLIT-EMPH ;
%<imp%>%<p2%>%<frm%>%<sg%>:%>%{I%}ң%{I%}з CLIT-EMPH ;
%<imp%>%<p2%>%<frm%>%<sg%>:%>%{I%}ң CLIT-EMPH ;                ! Dir/LR FIXME CHECK
%<imp%>%<p2%>%<frm%>%<pl%>:%>%{I%}ң%{I%}з%>д%{A%}р CLIT-EMPH ;

! Optative/jussive forms
%<opt%>%<p1%>%<sg%>:%>%{A%}й%{I%}н CLITICS-NO-COP ;
!%<opt%>%<p2%>%<sg%>:%>%{G%}%{I%}н CLITICS-NO-COP ;              ! FIXME check
%<opt%>%<p3%>%<sg%>:%>с%{I%}н CLITICS-NO-COP ;
%<opt%>%<p1%>%<pl%>:%>%{A%}й%{I%}%{K%} CLITICS-NO-COP ;
!%<opt%>%<p2%>%<pl%>:%>%{G%}%{I%}л%{A%} CLITICS-NO-COP ;         ! FIXME check
%<opt%>%<p3%>%<pl%>:%>с%{I%}н CLITICS-NO-COP ;

!%<opt%>%<p2%>%<frm%>%<sg%>:%>%{G%}%{I%}л%{A%} CLITICS-NO-COP ;  ! FIXME check
!%<opt%>%<p2%>%<frm%>%<pl%>:%>%{G%}%{I%}л%{A%} CLITICS-NO-COP ;  ! FIXME check

LEXICON V-COMMON

V-FINITE-REGULAR_NEGATIVE ;
%<neg%>:%>%{M%}%{A%} V-FINITE-REGULAR_NEGATIVE ;
%<neg%>%<gpr_past%>:%>%{M%}%{A%}%>%{I%}п CLITICS-NO-COP ; !Ұзақ жол жүріп ұйықтамап едім... !Dir/LR
                                                          !TODO this is a hack, doesn't feel right

V-FINITE-IRREGULAR_NEGATIVE ;

V-NONFINITE-REGULAR_NEGATIVE ;
%<neg%>:%>%{M%}%{A%} V-NONFINITE-REGULAR_NEGATIVE ;

V-NONFINITE-IRREGULAR_NEGATIVE ;

LEXICON V-DER

%<coop%>:%>%{I%}с V-COMMON ;

LEXICON V-TV-NOPASS

%<v%>%<tv%>: V-COMMON ;
%<v%>%<tv%>: V-DER ;

LEXICON V-TV-PASS

! e.g. көрін
%<v%>%<tv%>%<pass%>: V-COMMON ;

LEXICON V-TV

%<v%>%<tv%>: V-COMMON ;
%<v%>%<tv%>: V-DER ;
%<v%>%<tv%>%<pass%>:%>%{I%}%{l%} V-COMMON ;

LEXICON V-IV

%<v%>%<iv%>: V-COMMON ;
%<v%>%<iv%>: V-DER ;

!!!!!!!!!!!!!!!!!!!!!!     auxiliary verbs

LEXICON Vinfl-AUX-IMPF

%<vaux%>%<pres%>: V-PERS-S1 ; ! /жүр, жатыр, тұр, отыр/
%<vaux%>%<pres%>%<evid%>:% екен V-PERS-S1 ; ! e.g., "жатыр екенмін"

LEXICON Vinfl-AUX-IMPF-NEG

%<vaux%>%<neg%>%<pres%>: V-PERS-S1 ; ! /жатқан жоқ/
%<vaux%>%<neg%>%<pres%>%<evid%>:% екен V-PERS-S1 ; ! e.g., "жатқан жоқ екенмін"

LEXICON Vinfl-AUX

%<vaux%>: V-COMMON ; ! these four verbs in other tenses and all other aux. verbs

!!!!!!!!!!!!!!!!!!!!!!     copula

LEXICON Copula

е%<cop%>%<ger_past%>:екен GER-POSSESSIVES ;
е%<cop%>%<ger_perf%>:екен%>%{L%}%{I%}к GER-POSSESSIVES ;

! This is an evidential aorist form, not past -JNW
е%<cop%>%<aor%>%<evid%>:екен V-PERS-S1 ;
е%<cop%>%<neg%>%<aor%>%<evid%>:емес% екен V-PERS-S1 ;

е%<cop%>%<ifi%>:е%>%{D%}%{I%} V-PERS-S2 ;

е%<cop%>%<neg%>%<aor%>:емес V-PERS-S1 ;

!!!!!!!!!!!!!!!!!!!!!!     PRONOUN'S INFLECTION

!!!!!!!   P E R S O N A L

LEXICON PRON-P12SG-CASES

%<nom%>:ен CLITICS-NO-COP ;
%<gen%>:енің CLITICS-NO-COP ;
%<dat%>:аған CLITICS-NO-COP ;
%<acc%>:ені CLITICS-NO-COP ;
%<abl%>:енен CLITICS-NO-COP ;
%<loc%>:енде CLITICS-NO-COP ;
%<ins%>:енімен CLITICS-NO-COP ;
%<ins%>:еніменен CLITICS-NO-COP ; ! Dir/LR
%<gen%>%<subst%>:енікі%{n%} CASES ;
%<loc%>:ендегі ATTR-SUBST ;
%<sim%>:ендей CLITICS-NO-COP ;
%<advl%>:еніңше CLITICS-NO-COP ;

LEXICON PRON-P3SG-CASES

%<nom%>:л CLITICS-NO-COP ;
%<nom%>: CLITICS-NO-COP ; ! Dir/LR
%<gen%>:ның CLITICS-NO-COP ;
%<dat%>:ған CLITICS-NO-COP ;
%<acc%>:ны CLITICS-NO-COP ;
%<abl%>:дан CLITICS-NO-COP ;
%<loc%>:нда CLITICS-NO-COP ;
%<ins%>:нымен CLITICS-NO-COP ;
%<ins%>:ныменен CLITICS-NO-COP ; ! Dir/LR
%<gen%>%<subst%>:нікі%{n%} CASES ;
%<loc%>:ндағы ATTR-SUBST ;
%<sim%>:ндай CLITICS-NO-COP ;
%<advl%>:ныңша CLITICS-NO-COP ;


LEXICON PRON-PERS-INFL

CASES-ETC ;

LEXICON PRON-PERS

мен%<prn%>%<pers%>%<p1%>%<sg%>:м PRON-P12SG-CASES ;

сен%<prn%>%<pers%>%<p2%>%<sg%>:с PRON-P12SG-CASES ;

ол%<prn%>%<pers%>%<p3%>%<sg%>:о PRON-P3SG-CASES ;
! the following severely overgenerates.  Please don't uncomment it. -JNW 2018-06-28
!ол%<prn%>%<pers%>%<p3%>%<sg%>:а PRON-P3SG-CASES ;
ол%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>%+үшін:аның% үшін POST ; ! Dir/LR

біз%<prn%>%<pers%>%<p1%>%<pl%>:біз PRON-PERS-INFL ;
біз%<prn%>%<pers%>%<p1%>%<pl%>:біз%>дер PRON-PERS-INFL ; ! Dir/LR
біз%<prn%>%<pers%>%<p1%>%<pl%>%<advl%>:біздіңше CLITICS-NO-COP ;

сендер%<prn%>%<pers%>%<p2%>%<pl%>:сендер PRON-PERS-INFL ;
сендер%<prn%>%<pers%>%<p2%>%<pl%>%<advl%>:сендерше CLITICS-NO-COP ;

олар%<prn%>%<pers%>%<p3%>%<pl%>:олар PRON-PERS-INFL ;
олар%<prn%>%<pers%>%<p3%>%<pl%>%<advl%>:оларша CLITICS-NO-COP ;

сіз%<prn%>%<pers%>%<p2%>%<sg%>%<frm%>:сіз PRON-PERS-INFL ;
сіз%<prn%>%<pers%>%<p2%>%<sg%>%<frm%>%<advl%>:сіздіңше CLITICS-NO-COP ;
сіздер%<prn%>%<pers%>%<p2%>%<pl%>%<frm%>:сіздер PRON-PERS-INFL ;
сіздер%<prn%>%<pers%>%<p2%>%<pl%>%<frm%>%<advl%>:сіздерше CLITICS-NO-COP ;

!!!!!!!   D E M O N S T R A T I V E

! FIXME CHECK (CLITICS-INCL-COP vs CLIT) I can't imagine forms like "бумын, бусың" etc.
! in Tatar, but not sure how it works in Kazakh /I.S./
!-----
! Good idea is to link prn.dem.sim forms alternatively to CLIT and CASE,
! so that it's "adverbial reading" (e.g. "шундый каты кычкырды") doesn't
! receive <nom> tag /IS/

LEXICON PRON-DEM-INFL

%<gen%>:ның CLITICS-NO-COP ;
%<dat%>:ған CLITICS-NO-COP ;
%<acc%>:ны CLITICS-NO-COP ;
%<abl%>:дан CLITICS-INCL-COP ;
%<abl%>:нан CLITICS-INCL-COP ; ! Dir/LR FIXME actually only "осы" seems to take this.
%<loc%>:нда CLITICS-INCL-COP ;
%<gen%>%<subst%>:нікі%{n%} CASES ;
%<loc%>:ндагы ATTR-SUBST ;
%<sim%>:ндай CASES ;  ! FIXME: should have <det>, etc.
%<advl%>:лай CLITICS-INCL-COP ;
%<qnt%>:нша CLITICS-NO-COP ;         !

%<pl%>:%>%{L%}%{A%}р CASES-ETC ;
%<pl%>%<px3sp%>:%>%{L%}%{A%}р%{I%}%{n%} CASES ;

! Commented out till these forms get implemented in tat.lexc too
!%<TD%>:шалық CLITICS-NO-COP ;        ! "that much"  ! FIXME: check cont lex
!%<reas%>%<abl%>:ндықтан # ; ! "because of"  ! FIXME: not attested with осы?

! FIXME: 1) link the below to PRON-DEM-INFL if possible
!            (later, once we are certain about things)

LEXICON PRON-DEM-BUL

! FIXME CHECK (бұны vs мұны) Grammars I have list only forms with "б", but variants
! starting with "м" seem to be more frequent in the (wikipedia) corpus /I.S./
!-----
! <s>FIXME</s> (бұл%<prn%>%<dem%>%<pl%>:бұлар FULL-NOMINAL-INFLECTION ;) FULL-NOMINAL-INFLECTION might lead to
! overgeneration with p1,p2 possessives (they seem to take onle <px3sp> ending);
! on the other hand, with something like бу%<prn%>%<dem%>%<px3sp%>:монысы%{n%} CASES ;
! we loose things like бу<prn><dem><pl><px>:боларныкы /I.S./

бұл%<prn%>%<dem%>%<nom%>:бұл CLITICS-INCL-COP ;
бұл%<prn%>%<dem%>%<nom%>:бұ CLITICS-INCL-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<gen%>:бұның CLITICS-NO-COP ;
бұл%<prn%>%<dem%>%<gen%>:мұның CLITICS-NO-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<dat%>:бұған CLITICS-NO-COP ;
бұл%<prn%>%<dem%>%<acc%>:бұны CLITICS-NO-COP ;
бұл%<prn%>%<dem%>%<acc%>:мұны CLITICS-NO-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<abl%>:бұдан CLITICS-INCL-COP ;
бұл%<prn%>%<dem%>%<abl%>:мұнан CLITICS-INCL-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<loc%>:бұнда CLITICS-INCL-COP ;
бұл%<prn%>%<dem%>%<loc%>:мұнда CLITICS-INCL-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<ins%>:бұнымен CLITICS-NO-COP ;
бұл%<prn%>%<dem%>%<ins%>:бұныменен CLITICS-NO-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<ins%>:мұнымен CLITICS-NO-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<ins%>:мұныменен CLITICS-NO-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<gen%>%<subst%>:мұнікі%{n%} CASES ; ! FIXME: check "м" vs "б"
бұл%<prn%>%<dem%>%<loc%>:бұндағы ATTR-SUBST ;
бұл%<prn%>%<dem%>%<loc%>:мұндағы ATTR-SUBST ; ! Dir/LR
бұл%<prn%>%<dem%>%<sim%>:мұндай CASES ;
бұл%<prn%>%<dem%>%<sim%>:бұндай CASES ; ! Dir/LR
бұл%<prn%>%<dem%>%<sim%>%<px3sp%>:мұндай%{I%}%{n%} CASES ;
бұл%<prn%>%<dem%>%<sim%>%<pl%>:мұндайлар CASES ;
бұл%<prn%>%<dem%>%<sim%>%<pl%>%<px3sp%>:мұндайлары%{n%} CASES ;
бұл%<prn%>%<dem%>%<advl%>:былай CLITICS-INCL-COP ;
бұл%<prn%>%<dem%>%<advl%>:бұлай CLITICS-INCL-COP ; ! Dir/LR
бұл%<prn%>%<dem%>%<qnt%>:мұнша CLITICS-NO-COP ;
бұл%<prn%>%<dem%>%<px3sp%>:бұнысы%{n%} CASES ;

бұл%<prn%>%<dem%>%<pl%>:бұлар CASES-ETC ;
бұл%<prn%>%<dem%>%<pl%>%<px3sp%>:бұлары%{n%} CASES ;

LEXICON PRON-DEM-MINA

мына%<prn%>%<dem%>%<nom%>:мына CLITICS-INCL-COP ;
мына%<prn%>%<dem%>%<gen%>:мынаның CLITICS-NO-COP ;
мына%<prn%>%<dem%>%<dat%>:мынаған CLITICS-NO-COP ;
мына%<prn%>%<dem%>%<acc%>:мынаны CLITICS-NO-COP ;
мына%<prn%>%<dem%>%<abl%>:мынадан CLITICS-INCL-COP ;
мына%<prn%>%<dem%>%<loc%>:мынада CLITICS-INCL-COP ;
мына%<prn%>%<dem%>%<ins%>:мынамен CLITICS-NO-COP ;
мына%<prn%>%<dem%>%<ins%>:мынаменен CLITICS-NO-COP ; ! Dir/LR
!мына%<prn%>%<dem%>%<px%>:мынанікі%{n%} CASES ;
!мына%<prn%>%<dem%>%<loc%>:мынадағы ATTR-SUBST ;
мына%<prn%>%<dem%>%<sim%>:мынадай CASES ;
мына%<prn%>%<dem%>%<advl%>:мыналай CLITICS-INCL-COP ;
мына%<prn%>%<dem%>%<px3sp%>:мына%<prn%>%<dem%>%{S%}%{I%}%{n%} CASES ;

мына%<prn%>%<dem%>%<pl%>:мыналар CASES-ETC ;
мына%<prn%>%<dem%>%<pl%>%<px3sp%>:мыналары%{n%} CASES ;

LEXICON PRON-DEM-MINAU

мынау%<prn%>%<dem%>%<nom%>:мынау CLITICS-INCL-COP ;

LEXICON PRON-DEM-ANA

%<nom%>: CLITICS-INCL-COP ;
%<gen%>:ның CLITICS-NO-COP ;
%<dat%>:ған CLITICS-NO-COP ;
%<acc%>:ны CLITICS-NO-COP ;
%<abl%>:дан CLITICS-INCL-COP ;
%<loc%>:да CLITICS-INCL-COP ;
%<ins%>:мен CLITICS-NO-COP ;
%<ins%>:менен CLITICS-NO-COP ; ! Dir/LR
%<gen%>%<subst%>:нікі%{n%} CASES ;
%<loc%>:дағы ATTR-SUBST ;
%<sim%>:дай CASES ;
!%<adv%>:лай CLITICS-INCL-COP ;
!%<px3sp%>:сы%{n%} CASES ;

%<pl%>:лар CASES-ETC ;
%<pl%>%<px3sp%>:лар%>%{S%}%{I%}%{n%} CASES ;

LEXICON DemonstrativePronouns

PRON-DEM-BUL ;
PRON-DEM-MINA ;
PRON-DEM-MINAU ;
ана%<prn%>%<dem%>:ана PRON-DEM-ANA ;

сол%<prn%>%<dem%>%<nom%>:сол CLITICS-INCL-COP ;
сол%<prn%>%<dem%>:со PRON-DEM-INFL ;
сол%<prn%>%<dem%>%<ins%>:сонымен CLITICS-NO-COP ;
сол%<prn%>%<dem%>%<ins%>:соныменен CLITICS-NO-COP ; ! Dir/LR
сол%<prn%>%<dem%>%<px3sp%>:сонысы%{n%} CASES ;

ол%<prn%>%<dem%>%<nom%>:ол CLITICS-INCL-COP ;
ол%<prn%>%<dem%>:о PRON-DEM-INFL ;
ол%<prn%>%<dem%>%<ins%>:онымен CLITICS-NO-COP ;
ол%<prn%>%<dem%>%<ins%>:оныменен CLITICS-NO-COP ; ! Dir/LR
ол%<prn%>%<dem%>%<px3sp%>:онысы%{n%} CASES ;

осы%<prn%>%<dem%>%<nom%>:осы CLITICS-NO-COP ;
осы%<prn%>%<dem%>:осы PRON-DEM-INFL ;
осы%<prn%>%<dem%>%<ins%>:осымен CLITICS-NO-COP ;
осы%<prn%>%<dem%>%<ins%>:осыменен CLITICS-NO-COP ; ! Dir/LR
осы%<prn%>%<dem%>%<px3sp%>:осысы%{n%} CASES ;

! FIXME DISCUSS: Two below were lexicalized as conjunctional adverbs.
! -ндықтан affix is a feature of verbs as well though, so it will be handled as a
! productive derivation.

!ол%<prn%>%<dem%>%<reas%>%<abl%>:ондықтан # ; ! "because of that"
!сол%<prn%>%<dem%>%<reas%>%<abl%>:сондықтан # ; ! "because of that"


!!!!!!!   I N T E R R O G A T I V E

! Some of the words listed in the grammars I have as interrogative pronouns
! were placed in other root lexicons depending on what POS they substitute or
! act like syntactically. So (as planned for today):
! 1) "неше" is a <num><itg>;
! 2) "қайда, қайдан, қашан, қалай, қалайша" are <adv><itg> (the first three
! should link to a class containing location cases btw, as many current adverbs
! do);
! 3) "қанша" is added both as an interrogative pronoun and as an interrogative
! adverb /IS/ and det.qnt /JNW/
!
! What already added as interrogative pronouns are: кім, ни, нәрсе
! And to be added as interrogative pronouns are: қайсы, қандай, қанша.
! I'd like to mark қандай as <sim> form (with қандай for the upper side though),
! but wouldn't go too deeply into the structure of other two and not derive them
! from "қай" (that's what they have most likely originated from) IS
!
! Of course, "қай", "қайсы" and all other pronouns which can be used as an
! attribute were added as determiners linking to CLIT as well.
! Good idea is to have an rlx rule selecting determiner reading when a word is
! ambiguous between (prn X nom) and (det X) and there is noun to the right;
! and selecting pronoun if there is a verb to the right.

LEXICON PRON-ITG-INFL

%<prn%>%<itg%>: FULL-NOMINAL-INFLECTION ;


LEXICON PRON-ITG-QAYSI-CASES

%<nom%>: CLITICS-NO-COP ;
%<gen%>:ның CLITICS-NO-COP ;
%<dat%>:ған CLITICS-NO-COP ;
%<acc%>:ны CLITICS-NO-COP ;
%<abl%>:дан CLITICS-NO-COP ;
%<loc%>:да CLITICS-NO-COP ;
%<ins%>:мен CLITICS-NO-COP ;
%<ins%>:менен CLITICS-NO-COP ; ! Dir/LR

LEXICON PRON-ITG-QAYSI

%<prn%>%<itg%>: PRON-ITG-QAYSI-CASES ;
%<prn%>%<itg%>: GER-POSSESSIVES ;

LEXICON PRON-ITG-QANSHA

! Might be not complete (қаншалар?)
! Currently қанша<prn><itg> is translated with ничә<num><itg><subst> and
! қанша<adv><itg> is going to be translated with "күпме" or "никадәр"

%<prn%>%<itg%>: CASES ;

LEXICON PRON-ITG-QANDAY

! Might be not complete (қандайларығыз? and possessive stuff in general)

%<prn%>%<itg%>%<sim%>: CLITICS-NO-COP ;
! ^ FIXME Similative form of demonstrative pronouns should receive an alternati-
! ve continuation with CLIT too. Consider e,g.: "Ең жақсы Интернет қандай?" Here
! it shouldn't receive <nom> tag I think /IS/
%<prn%>%<itg%>%<sim%>: CASES ;
%<prn%>%<itg%>%<sim%>%<px3sp%>:ы%{n%} CASES ;
%<prn%>%<itg%>%<sim%>%<pl%>:лар CASES ;
%<prn%>%<itg%>%<sim%>%<pl%>%<px3sp%>:лары%{n%} CASES ;

LEXICON InterrogativePronouns

қайсы:қайсы PRON-ITG-QAYSI ;
қанша:қанша PRON-ITG-QANSHA ;
қандай:қандай PRON-ITG-QANDAY ;
нәрсе:нәрсе PRON-ITG-INFL ; ! "thing" ! Use/MT
не:не PRON-ITG-INFL ; ! "what"
немене:немене PRON-ITG-INFL ; ! "what"
кім:кім PRON-ITG-INFL ; ! "who"

LEXICON PRON-QNT-POSS

%<prn%>%<qnt%>: GER-POSSESSIVES ;

LEXICON PRON-QNT

%<prn%>%<qnt%>: GER-INFL ;

LEXICON PRON-IND

%<prn%>%<ind%>: CASES ;

LEXICON PRON-IND-PL

%<prn%>%<ind%>%<pl%>: CASES ;


LEXICON PRNNEG

%<prn%>%<neg%>: CASES ;

LEXICON PRON-RECIP

%<prn%>%<recip%>: GER-POSSESSIVES ;

LEXICON PRON-REF-ADV

CASES-ETC ;
%<advl%>:ше CLITICS-NO-COP ;

LEXICON PRON-REF

! FIXME JNW please have a look at this
! Listing all forms here because of overgeneration with *өздерім,
! *өздеріміз, *өзіңдер etc while linking to POSSESSIVES. This was the idea.
! Now they are handled more like personal pronouns (the same tags in the same
! order), so they should pass transfer without any trouble.
!-----
! A remaining problem with reflexive pronoun is that it takes personal copula
! suffixes "selectively": e.g. prn.refl.px1sg would take only cop.px1sg
! (prn.ref.px3sp seems to take all of them though).
! A little bit of overgeneration won't hurt anyone :), but I think that odd
! forms could be filtered out somehow later. Don't know how to do it./04Aug, IS/

! FIXME DISCUSS Write full forms for the lower side and put only PRON-DEF in the
! Root Lexicon

%<prn%>%<ref%>%<px1sg%>:%>%{I%}м PRON-REF-ADV ;
%<prn%>%<ref%>%<px2sg%>:%>%{I%}ң PRON-REF-ADV ;
%<prn%>%<ref%>%<px3sp%>:%>%{S%}%{I%}%{n%} PRON-REF-ADV ; ! FIXME <px3sg>?
%<prn%>%<ref%>%<px1pl%>:%>%{I%}м%{I%}з PRON-REF-ADV ;
%<prn%>%<ref%>%<px2pl%>:%>д%{A%}р%>%{I%}ң PRON-REF-ADV ;
%<prn%>%<ref%>%<px3pl%>:%>д%{A%}р%>%{S%}%{I%}%{n%} PRON-REF-ADV ;

%<prn%>%<ref%>%<px2sg%>%<frm%>:%>%{I%}ң%{I%}з PRON-REF-ADV ;
%<prn%>%<ref%>%<px2pl%>%<frm%>:%>д%{A%}р%>%{I%}ң%{I%}з PRON-REF-ADV ;

!!!!!!!!!!!!!!!!!!!!!!     DETERMINERS

LEXICON DET-DEM

%<det%>%<dem%>: # ;

LEXICON DET-QNT

%<det%>%<qnt%>: # ;

LEXICON DET-IND

%<det%>%<ind%>: # ;

LEXICON DET-ITG

%<det%>%<itg%>: # ;

LEXICON DET-NEG

%<det%>%<neg%>: # ;

LEXICON DET-REF

%<det%>%<ref%>: # ;


!!!!!!!!!!!!!!!!!!!!!!     OTHER PART-OF-SPEECH LEXICONS

LEXICON N1-ABBR

%<n%>%<attr%>: # ;
%<n%>:%- POSSESSIVES ;
%<n%>:%- CASES-ETC ;
%<n%>: CASES-ETC ; ! Dir/LR


LEXICON N1

%<n%>%<attr%>: # ;
%<n%>: FULL-NOMINAL-INFLECTION ;

LEXICON N1-Ә

:%{ә%} N1 ;            ! generate front for {I} and back for {A} - куәні, куәға
:ә%{ъ%} N1 ;  ! Dir/LR ! analyse all back forms
:ә N1 ;       ! Dir/LR ! analyse all front forms

LEXICON N-COMPOUND-PX-COMMON
 
:%>%{S%}%{I%}%{n%} CASES-ETC ;
POSSESSIVES ;       ! for stuff like "біздің ауа райымыз" and even "оның ауа райы",
                  ! which should have a <px3sp> analysis


LEXICON N-COMPOUND-PX ! consider "N2" for the name
                      ! equiv of kir:LEXICON N-INFL-3PX-COMPOUND

%<n%>: N-COMPOUND-PX-COMMON ;
%<n%>%<pl%>:%>%{L%}%{A%}р%>%{S%}%{I%}%{n%} CASES-ETC ; ! (e.g. чик сакчылары=пограничники)
                                                      !  FIXME CHECK: Is {S} needed here ?

LEXICON N-COMPOUND-PX-PL
! for things like Олимпиада ойындары

%<n%>%<pl%>: N-COMPOUND-PX-COMMON ; ! FIXME: do we want these to get <pl>??


LEXICON N3
! Singularia tantum

%<n%>%<attr%>: # ;
%<n%>: FULL-NOMINAL-INFLECTION ;

LEXICON N5

:%{☭%} N1 ;

LEXICON N6

:%{і%} N1 ; 
N1 ;  ! Dir/LR     ! We want to analyse forms without %{і%}, since they're used
                   ! but "properly" we want to generate forms with %{і%}

LEXICON N-INFL-NKI

%<n%>:%{I%}н KI ; ! Dir/LR - common variant in both Kazakh and Kyrgyz
%<n%>:%{I%}ң KI ; 
%<n%>: FULL-NOMINAL-INFLECTION ;


!LEXICON NAT
! Nationalites

!A1 ;              ! nationality's adjectival form, e.g. "Turkish [food]" ! Consider <n.attr> reading for this 
!N1 ;              ! a member of the nationality, e.g. "A Turk"
!:%>ш%{A%} ADV ;   ! as / in the style of the nat., e.g. "à la Turk"
!:%>ш%{A%} ADV ;   ! in the language of the nationality, e.g. "in Turkish" ! I guess ADJ was meant /I.S./
!:%>ш%{A%} N1 ;    ! the language of the nationality, e.g. "Turkish"

LEXICON ADV-LANG

N1 ;
ADV ;

LEXICON NP-COMMON
FULL-NOMINAL-INFLECTION ;

LEXICON NP-TOP-RUS

%<np%>%<top%>:%{☭%} NP-COMMON ;
%<np%>%<top%>%<attr%>: # ;

LEXICON NP-TOP

%<np%>%<top%>: NP-COMMON ;
%<np%>%<top%>%<attr%>: # ;

!%<adj%>%<top%>:%{L%}%{I%}%{K%} ADJ-COMMON ;  ! FIXME: do these tags make sense?

LEXICON NP-TOP-ASSR

%<np%>%<top%>%<attr%>: # ;

%<np%>%<top%>: NP-COMMON ; ! Dir/LR
%<np%>%<top%>:%- NP-COMMON ;

LEXICON NP-TOP-COMPOUND

%<np%>%<top%>: N-COMPOUND-PX-COMMON ;
%<np%>%<top%>%<attr%>:%>%{s%}%{I%}%{n%} # ;

LEXICON NP-TOP-ABBR

%<np%>%<top%>%<attr%>: # ;
%<np%>%<top%>%<nom%>: CLITICS-INCL-COP ;
%<np%>%<top%>:%- NP-COMMON ;

%<np%>%<top%>: NP-COMMON ;    ! Dir/LR   ! also allow like this to be parsed, but generate with -


LEXICON NP-ANT-M

%<np%>%<ant%>%<m%>: NP-COMMON ;
%<np%>%<ant%>%<m%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-ANT-F

%<np%>%<ant%>%<f%>: NP-COMMON ;
%<np%>%<ant%>%<f%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-COG-M

%<np%>%<cog%>%<m%>: NP-COMMON ;
%<np%>%<cog%>%<m%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-COG-MF

! Dual-gender cognoms.
! Don't derive anything /I.S./

%<np%>%<cog%>%<mf%>: NP-COMMON ;
%<np%>%<cog%>%<mf%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-COG-OBIN-FEM

%<np%>%<cog%>%<f%>:а NP-COMMON ;
%<np%>%<cog%>%<f%>%<pl%>:а%>%{L%}%{A%}р NP-COMMON ;


LEXICON NP-COG-OB

! For cognoms ending with -ов, -ев
! They derive feminine equivalets taking -а /I.S./
! Also, even the ев ones are normally pronounced
!    (and take harmony) as if they end in йыф/йып

%<np%>%<cog%>%<m%>:%{☭%} NP-COMMON ;                       ! Dir/LR
%<np%>%<cog%>%<m%>%<pl%>:%{☭%}%>%{L%}%{A%}р NP-COMMON ;    ! Dir/LR

%<np%>%<cog%>%<m%>:%{а%}%{с%} NP-COMMON ;
%<np%>%<cog%>%<m%>%<pl%>:%{а%}%{с%}%>%{L%}%{A%}р NP-COMMON ;

NP-COG-OBIN-FEM ;


LEXICON NP-COG-IN

! For cognoms ending with -ов, -ев, -ин
! They derive feminine equivalets taking -а /I.S./

%<np%>%<cog%>%<m%>:%{☭%} NP-COMMON ;
%<np%>%<cog%>%<m%>%<pl%>:%{☭%}%>%{L%}%{A%}р NP-COMMON ;

NP-COG-OBIN-FEM ;

! LEXICON NPCOGFLEX

! This isn't used at the moment,
! but e.g. ?Polish cognoms ending with -ска would take this cont.class /I.S./ 

LEXICON NP-PAT-VICH

! For patronyms ending with -вич
! Their feminine equivalets end with -вна /I.S./

%<np%>%<pat%>%<m%>:вич%{☭%} NP-COMMON ;

%<np%>%<pat%>%<f%>:вна NP-COMMON ;

! WAS: (JNW 2015-01-17)
!LEXICON NP-ORG
!
!%<np%>%<org%>: CASES ; 

LEXICON NP-ORG

%<np%>%<org%>%<nom%>: CLITICS-INCL-COP ;
%<np%>%<org%>: FULL-NOMINAL-INFLECTION-NONOM ;
%<np%>%<org%>%<attr%>: # ;

LEXICON NP-ORG-LAT

%<np%>%<org%>%<nom%>: CLITICS-INCL-COP ;
%<np%>%<org%>: FULL-NOMINAL-INFLECTION-NONOM ; ! Dir/LR
%<np%>%<org%>:%- FULL-NOMINAL-INFLECTION-NONOM ;
! This one doesn't work right because of twol:
!%<np%>%<org%>:%' FULL-NOMINAL-INFLECTION-NONOM-NONOM ; ! Dir/LR
%<np%>%<org%>%<attr%>: # ;

LEXICON NP-ORG-COMPOUND 

%<np%>%<org%>: N-COMPOUND-PX-COMMON ;   ! COMMON because no plural

LEXICON NP-ORG-ABBR             ! for abbreviations like FIFA
%<np%>%<org%>%<attr%>: # ;
%<np%>%<org%>%<nom%>: CLITICS-INCL-COP ;
%<np%>%<org%>:%- NP-COMMON ;
%<np%>%<org%>: NP-COMMON ;    ! Dir/LR   ! also allow like this to be parsed, but generate with -

LEXICON NP-AL

%<np%>%<al%>: CASES ;

LEXICON NP-AL-ABBR              ! for abbreviations like ӘЧ - Әлем Чемпионаты
%<np%>%<al%>%<attr%>: # ;
%<np%>%<al%>%<nom%>: CLITICS-INCL-COP ;
%<np%>%<al%>:%- NP-COMMON ;
%<np%>%<al%>: NP-COMMON ;    ! Dir/LR   ! also allow like this to be parsed, but generate with -


LEXICON NUM-COMMON

%<num%>: # ;
%<num%>%<subst%>: FULL-NOMINAL-INFLECTION ;

LEXICON NUM

NUM-COMMON ;
%<num%>%<ord%>:%>%{I%}нш%{I%} # ;             ! FIXME: base form, <det> reading
%<num%>%<ord%>%<subst%>:%>%{I%}нш%{I%} FULL-NOMINAL-INFLECTION ;

! <ord> is "det" is base form, <ord><subst> is "prn" reading

LEXICON NUM-TWENTY

NUM-COMMON ;
%<num%>%<ord%>:%>с%{I%}нш%{I%} # ;
%<num%>%<ord%>%<subst%>:%>с%{I%}нш%{I%} FULL-NOMINAL-INFLECTION ;

LEXICON NUM-ITG ! This is a temporal solution
                ! (could be integrated with the above)

%<num%>%<itg%>: # ;
%<num%>%<itg%>%<subst%>: FULL-NOMINAL-INFLECTION ;
%<num%>%<itg%>%<ord%>:%>%{I%}нш%{I%} FULL-NOMINAL-INFLECTION ;

LEXICON NUM-COLL

%<num%>%<coll%>%<subst%>: FULL-NOMINAL-INFLECTION ;
%<num%>%<coll%>%<advl%>: # ;

LEXICON NUM-ROMAN

%<num%>%<ord%>: # ;

LEXICON LTR

%<ltr%>: # ;

!LEXICON DIGITLEX
!
!%<num%>: # ;

LEXICON POST

%<post%>: CLITICS-NO-COP ;

LEXICON POSTADV

%<postadv%>: # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON KI

%<attr%>:%>%{G%}%{I%} CLITICS-NO-COP ;
%<subst%>:%>%{G%}%{I%} FULL-NOMINAL-INFLECTION ;

LEXICON ADV

%<adv%>: CLITICS-NO-COP ;

LEXICON ADV-WITH-KI

ADV ;
%<adv%>: KI ;
%<adv%>%<abl%>:%>%{D%}%{A%}н # ; ! FIXME: overgenerates a bit

LEXICON ADV-WITH-KI-I     ! Used for бері<adv>/бергі<adv><attr> 'right now' and similar

:%{I%} ADV ;
%<adv%>: KI ;

LEXICON ADV-ITG

%<adv%>%<itg%>: CLITICS-INCL-COP ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON CC

%<cnjcoo%>: CLIT-GHANA-ETC ;

LEXICON CS

%<cnjsub%>: # ; 

LEXICON CA

%<cnjadv%>: CLIT-GHANA-ETC ;

LEXICON QST

%<qst%>: # ;

LEXICON MOD-ASS

%<mod_ass%>: # ;

LEXICON MOD

! шығар, сияқты etc.

%<mod%>: # ;

LEXICON INTERJ

%<ij%>: # ;

LEXICON IDEO

%<ideo%>: # ;

LEXICON ABBR

%<abbr%>: # ;

LEXICON PAREN

%<paren%>: # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!============!
 LEXICON Root
!============!

._R_NKT:. # ;
